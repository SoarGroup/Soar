epmem --set learning on
epmem --set trigger none
epmem --set inclusions bar
watch --epmem

sp {propose*init
   (state <s> ^superstate nil
            -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^name test-inclusions)
}

sp {propose*do-storage
   (state <s> ^name test-inclusions
             -^base)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-storage)
}

sp {apply*do-storage
   (state <s> ^operator.name do-storage
              ^epmem.command <cmd>)
-->
   (<s> ^base <base>)
   (<base> ^bar <bar> ^jaz <jaz>)
   (<bar> ^chef <chef> ^foo <foo>)
   (<chef> ^x 3 ^y 4 ^foo 0)
   (<foo> ^x 5 ^y 6)
   (<jaz> ^x 1 ^y 2)

   (<cmd> ^store true)
}

sp {propose*do-query-fail
   (state <s> ^name test-inclusions
              ^epmem <epmem>)
   (<epmem> ^result.success true
            ^command <cmd>)
   (<cmd> ^store true)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-query-fail)
}

sp {apply*do-query-fail
   (state <s> ^operator.name do-query-fail
              ^epmem <epmem>)
   (<epmem> ^command <cmd>)
   (<cmd> ^store true)
-->
   (<cmd> ^store true -)
   (<cmd> ^query <q>)
   (<q> ^base.jaz.x 1)
}

sp {propose*epmem-failure*should-not-have-found-this-memory
   (state <s> ^name test-inclusions
              ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result <result>)
   (<result> ^success <q>)
   (<q> ^base.jaz.x)
-->
   (<s> ^operator <o> +)
   (<o> ^name epmem-failure
        ^reason |Should not have found the un-whitelisted memory.|)
}

sp {propose*do-query-success
   (state <s> ^name test-inclusions
              ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure <q>)
   (<q> ^base.jaz.x)
-->
   (<s> ^operator <o> +)
   (<o> ^name do-query-success)
}

sp {apply*do-query-success
   (state <s> ^operator.name do-query-success
              ^epmem <epmem>)
   (<epmem> ^command <cmd>)
   (<cmd> ^query <old-query>)
-->
   (<cmd> ^query <old-query> - <q>)
   (<q> ^base.bar.chef.x 3)
}

sp {propose*epmem-success
   (state <s> ^name test-inclusions
              ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result <result>)
   (<result> ^success <q>
             ^retrieved <memory>)
   (<q> ^base.bar.chef.x 3)
   (<memory> ^base <base>)
   (<base> ^bar <bar> -^jaz)
   (<bar> ^chef <chef> ^foo <foo>)
   (<chef> ^x 3 ^y 4 ^foo 0)
   (<foo> ^x 5 ^y 6)
-->
   (<s> ^operator <o> +)
   (<o> ^name epmem-success)
}

sp {propose*epmem-failure*failure
   (state <s> ^name test-inclusions
              ^epmem <epmem>)
   (<epmem> ^command.query <q>
            ^result.failure <q>)
   (<q> ^base.bar.chef.x 3)
-->
   (<s> ^operator <o> +)
   (<o> ^name epmem-failure
        ^reason |result.failure|)
}

sp {apply*epmem-success
   (state <s> ^operator.name epmem-success)
-->
   (write |Success!|)
   (succeeded)
}

sp {apply*epmem-failure
   (state <s> ^operator <o>)
   (<o> ^name epmem-failure
        ^reason <reason>)
-->
   (write |Failure: | <reason>)
   (failed)
}
