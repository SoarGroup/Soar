sp {scout*propose*unhide
   (state <s> ^name scout
             -^top-state.entities.entity.area <a>
              ^top-state.current-waypoint <wh>)
   (<wh> ^hiding <w>)
   (<w> ^hide <wh>)
   (<wh> ^area <a>)
-->
   (<s> ^operator <op> +)
   (<op> ^name unhide
         ^waypoint <w>)
}

sp {scout*compare*unhide
   (state <s> ^name scout
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name unhide)
   (<op2> ^name rotate-to)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {apply*unhide*note-not-hiding
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name unhide)
   (<ts> ^hiding true)
-->
   (<ts> ^hiding true -)
   (write (crlf) |Unhiding|)
   (exec player-print |Unhiding|)
}


sp {apply*unhide*destination-waypoint
   (state <s> ^operator <op>
              ^io.output-link <ol>
              ^parameters.linear-velocity <thr>)
   (<op> ^name unhide
         ^move-now true
         ^waypoint <h>)
   (<h> ^x <x>
        ^y <y>)
-->
   (<ol> ^add-waypoint <mt>)
   (<mt> ^x <x>
         ^y <y>)
}
