### Program  analogy.toptwo.soar -- Top two spaces of analogy.soar
### Started  5 Aug 93
### Version  18 Nov 94, based on analogy.soar v. 1 Nov 94 -- original
###          by rmy
### NB:      nnpscm version  -- conversion by John Rieman
### Version  5 Dec 94, some concessions to improving the trace
###          in NNPSCM -- rmy
### Converted to Soar 7 by unknown hand
### Some minor cleaning up, RMY 3 Mar 96
### Some minor cleaning up and move to 7.0.4, 2-Nov-96 -FER

### Program to do simple 'deliberate analogy' for figuring out how to
### launch a program on the Mac, given that one or two specific ones
### are known.

### Note that this program does NOT want the default rules loaded.

### This version developed specifically for the purposes of the one-day
### Soar tutorial given by Frank Ritter and Richard Young.  A set of
### Macintosh files containing OHP slides together with exercises and
### other handouts (either describing just this program, else covering
### the whole one-day tutorial) can be obtained by contacting
###	ritter@psyc.nott.ac.uk

### Note that I have used state-elaboration rules instead of operators in
### many places, probably to a far greater extent than is justified in
### terms of cognitive modelling, but it shortens the program and perhaps
### makes its behaviour easier to follow.

### Correspondingly, there are several highly specific monitoring rules
### present, which we would not normally expect to see in this kind of
### program.


### ======================================================================
### DECLARATIONS, INITIALISATIONS, ETC.
### ======================================================================

source soar-menu.tcl

###
echo "Initializing Soar."
init-soar

## The default is not to print chunks as learned, and we want to see them.
watch learning -print

## This command makes chunk firings print.
watch -chunks -print

## This will always take the first choice.
indifferent-selection -first

## For safty, excise -all
excise -all

### This format is specifically for monitoring the Imagine-Task space

### For NNPSCM we do the following, which is intended
### for watch 0 format, might be a little weird in anything else:

format-watch -stack -add s {%right[6,%dc]: %rsd[   ]==>S: %cs %rsd[   ]}

sp {monitor*problem-space*nnpscm
  (state <s> ^problem-space (<p> ^name <n>))
  -->
  (write |      P: | <p> |(| <n> |)|)}

sp {monitor*problem-space*imagine-task
  (state <s> ^problem-space <p>)
  (<p> ^type imaginary ^alias imagine-task)
  -->
  (write | type = imaginary  alias = imagine-task|)}


### Ask what program to launch
## deleted cg and xl from list because not enough knowledge!
## 28-Mar-96 -FER

## the non TK version
if {[info commands tk]==""} {
sp {preamble*ask-program-to-launch
  (state <s> ^name perform)
  (<s> ^task <t>)
	(<t> ^feature <f2>)
	(<f2> ^fname arg1)
  -->
  (write (crlf) |What program to launch? [word, draw] |)
  (<f2> ^fvalue (accept))}
}

## the TK version
if {[info commands tk]!=""} {
sp {preamble*ask-program-to-launch
  (state <s> ^name perform)
  (<s> ^task <t>)
	(<t> ^feature <f2>)
	(<f2> ^fname arg1)
  -->
  (write (crlf) |What program to launch? |)
  (<f2> ^fvalue
       (tcl |soar-menu "Select program to launch" {word draw}|))}
}


### ======================================================================
### PERFORM SPACE
### ======================================================================

### -- Initialisation --

sp {perform*initialise*gps
  (state <s> ^superstate nil)
  -->
  (<p> ^name perform)
  (<s> ^name perform ^problem-space <p>)}

sp {perform*initialise*state
  (state <s> ^name perform)
  -->
  (<s> ^task <t>)
	(<t> ^feature <f1> + <f1> &, <f2> + <f2> &)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1)}


### -- Termination --

### Kludgy-detect when a program has been successfully launched

sp {perform*task*terminate
  (state <s> ^problem-space.name perform)
  (<s> ^task <t>)
	(<t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1   ^fvalue <prog>)
  (<s> ^simulation <sim>)
  (<sim> ^status running ^object <prog>)
  -->
  (write (crlf) |** Yeah, the program is running!|)
  (halt)}


### -- Performing actions --

### If there's ever an Action attribute on the state, then perform the
### action.

### Issues about how to know when an action has been completed,
### what happens if there is more than one action proposed, and so on,
### are beyond the scope of this demonstation program (and hopefully will
### not arise).

### Propose a perform operator

sp {perform*perform*propose
  (state <s> ^problem-space.name perform)
  (<s> ^action <a>)
  -->
  (<o> ^name perform ^action <a>)
  (<s> ^operator <o>)}


### Performing the double-click action

sp {perform*perform*double-click*perform
  (state <s> ^problem-space.name perform ^operator <o>)
  (<o> ^name perform ^action <a>)
  (<a> ^movement double-click ^object <obj>)
  -->
  (<s> ^motor <m>)
  (<m> ^movement double-click ^object <obj>)
  (write (crlf) |** User double-clicks on object | <obj>)}


### -- Kludgy simulation of Mac launching programs --

### The following rule would be more elegant if it knew what objects
### were 'isa' program

sp {perform*simulate*launch*program
  (state <s> ^problem-space.name perform ^operator.name <oname>)
  (<s> ^motor <m>)
  (<m> ^movement double-click ^object <obj> ^object << cg word draw xl >>)
  -->
  (<s> ^simulation <sim>)
  (<sim> ^status running ^object <obj>)
  (write (crlf) |** Mac now has program | <obj> | running|)}


### -- Just for testing --

### Just for testing, the following hand-written chunk proposes the action
### of double-clicking on a program to be launched.  Commented out.

#
#sp {perform*task*action*double-click-to-launch
#  (state <s> ^problem-space.name perform)
#  (<s> ^task <t>)
#	(<t> ^feature <f1> <f2>)
#	(<f1> ^fname effect ^fvalue launch)
#	(<f2> ^fname arg1   ^fvalue <prog>)
#  -->
#  (<s> ^action <a>)
#  (<a> ^movement double-click ^object <prog>)}
#

### Monitor ^task and ^action on the state

sp {perform*monitor*state*task
  (state <s> ^problem-space.name perform)
  (<s> ^task <t>)
	(<t> ^feature <f1> {<> <f1> <f2>})
	(<f1> ^fname effect ^fvalue <effect>)
	(<f2> ^fname arg1   ^fvalue <prog>)
  -->
  (write (crlf) |State has ^task: effect=| <effect> | arg1=| <prog>)}

sp {perform*monitor*state*action
  (state <s> ^problem-space.name perform)
  (<s> ^action <a>)
	(<a> ^movement <move> ^object <obj>)
  -->
  (write (crlf) |State has ^action: | <move> | | <obj>)}


### ======================================================================
### ACTION-PROPOSAL SPACE
### ======================================================================

### This is where we drop down to when no action is proposed in the Perform
### space for a given task.  The impasse shows up as a State-No-Change.

### -- Initialise --

### Check that there's a task but no action  ... Oh no!!  That's going to
### give rise to self-undermining chunks.  For now, check just that there's
### a task, we'll assume no action.

sp {action-proposal*initialise
  (state <s> ^superstate <ss> ^impasse no-change ^attribute state)
  (<ss> ^problem-space.name perform)
  (<ss> ^task <t>)
  -->
  (<s> ^problem-space <p>)
  (<p> ^name action-proposal)}


### -- Assumed knowledge --

### Here we hand-code the assumed knowledge about how to launch Word or Draw
### by double-clicking.  When used, this will give rise to specific chunks
### back in the Perform space.

### If the task is to launch Word or Draw, then propose an action to
### double-click.

sp {action-proposal*double-click-to-launch
  (state <s> ^problem-space.name action-proposal ^superstate <ss>)
  (<ss> ^task <t>)
	(<t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1   ^fvalue <prog> ^fvalue << word draw >>)
  -->
  (<ss> ^action <a>)
  (<a> ^movement double-click ^object <prog>)}


### -- Use analogy --

### The only "method" we have is to try using analogy.  We'll do this by
### proposing the use-analogy operator, which will implement in its own space.

sp {action-proposal*use-analogy*propose
  (state <s> ^problem-space.name action-proposal)
  -->
  (<o> ^name use-analogy)
  (<s> ^operator <o>)}


### ======================================================================
### EOF
### ======================================================================


