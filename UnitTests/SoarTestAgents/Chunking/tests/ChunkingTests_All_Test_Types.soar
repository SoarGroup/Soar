# Tests:
# - All relational test types
# - Includes literal relational test and disjunction
# - RHS actions that are variablized
#-  RHS actions with literals that are the same symbols
#   as were variablized. 
srand 23
chunk always

sp {init-superstate
    (state <s> ^superstate nil)
-->
    (<s> ^i1 1 ^i2 2 ^i3 3 ^i4 4)
    (write (crlf) |Starting chunking unit test.|)
}

sp {propose*top
    (state <s> ^superstate nil)
-->
    (<s> ^operator <o>)
    (<o> ^name do-test)
}

sp {apply*test-done
    (state <s> ^superstate nil ^test-done)
-->
    (write (crlf) |Chunk produced. Test completed.|)
    (succeeded)
    (interrupt)
}

sp {apply*test-failed
    (state <s> ^superstate.superstate.superstate.superstate nil)
-->
    (write (crlf) |Chunk not produced.  Test failed.|)
    (interrupt)
}

sp {init-substate
    (state <s> ^superstate.superstate nil)
-->
    (<s> ^name substate)}

sp {propose*test
    (state <s> ^name substate)
-->
    (<s> ^operator <o> +)
    (<o> ^name test)}

sp {apply2
    (state <s> ^name substate
               ^operator.name test
               ^superstate <ss>)
    (<ss> ^i1 {< <x2> >= <x1> < 2 <x1>}
          ^i2 {> <x1> <= <x2> < <x3> > 1 < 23 <x2>}
          ^i3 {<=> <x1> <> 4 <> <ss> <x3>}
          ^i4 {<< 4 9 >> > <x2> <x4>})
-->
    (<ss> ^x1 <x1> ^x2 <x2> ^x3 <x3> ^x4 <x4>
          ^c1 1 ^c2 2 ^c3 3 ^c4 4 ^c5 3.0
          ^unbound <u>)
    (<u> ^x1 <x1> ^x2 <x2> ^x3 <x3> ^x4 <x4>
          ^c1 1 ^c2 2 ^c3 3 ^c4 4 ^c5 3.0)
}

#run
#ctf expected/chunk2.soar print -cf