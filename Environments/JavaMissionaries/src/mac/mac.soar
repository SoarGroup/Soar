learn --off

###
### STATE SUCCESS/GOAL DETECTION
###

sp {mac*detect*state*success
   (state <s> ^desired <d>
              ^<side> <ls>)
   (<ls> ^missionaries <m>
         ^cannibals <c>)
   (<d> ^{ << right-bank left-bank >> <side> } <dls>)
   (<dls> ^missionaries <m>
          ^cannibals <c>)
-->
   (write (crlf) |The problem has been solved.|)
   (halt)}

###
### STATE FAILURE DETECTION
###

sp {mac*evaluate*state*failure*more*cannibals
   (state <s> ^desired <d>
              ^<< right-bank left-bank >> <bank>)
   (<bank> ^missionaries { <n> > 0 }
           ^cannibals > <n>)
-->
   (write (crlf) |Failure State.|)
   (<s> ^failure <d>)}

###
### move-BOAT MONITOR OPERATOR AND STATE
###

sp {monitor*move-boat
   (state <s> ^operator <o>)
   (<o> ^name move-boat
        ^{ << cannibals missionaries >>  <type> } <number>)
   -->
   (write (crlf) | Move | <number> | | <type>)}

sp {monitor*state*left
   (state <s> ^name mac
              ^left-bank <l>
              ^right-bank <r>)
   (<l> ^missionaries <ml>
        ^cannibals <cl>
        ^boat 1)
   (<r> ^missionaries <mr>
        ^cannibals <cr>
        ^boat 0)
   -->
   (write (crlf) | M: | <ml> |, C: | <cl> | B ~~~   | 
                 | M: | <mr> |, C: | <cr> |  |)}

sp {monitor*state*right
   (state <s> ^name mac
              ^left-bank <l>
              ^right-bank <r>)
   (<l> ^missionaries <ml>
        ^cannibals <cl>
        ^boat 0)
   (<r> ^missionaries <mr>
        ^cannibals <cr>
        ^boat 1)
   -->
   (write (crlf) | M: | <ml> |, C: | <cl> |   ~~~ B | 
                 | M: | <mr> |, C: | <cr> |  |)}

## If failure, undo last opertor

sp {mac*select*operator*prefer*inverse*failure*types*1
   (state <s> ^name mac
              ^operator <o> +
              ^failure <d>
              ^last-operator <lo>)
   (<o> ^name move-boat
        ^<type> <number>
        ^types 1)
   (<lo> ^types 1
         ^type <type>
         ^number <number>)
-->
   (<s> ^operator <o> >)}

sp {mac*select*operator*prefer*inverse*failure*types*2
   (state <s> ^name mac
              ^operator <o> +
              ^failure <d>
              ^last-operator.types 2)
   (<o> ^types 2)
-->
   (<s> ^operator <o> >)}

## If not failure, avoid last operator

sp {mac*select*operator*avoid*inverse*not*failure*1
   (state <s> ^name mac
              ^operator <o> +
             -^failure <d>
              ^last-operator <lo>)
   (<o> ^types 1
        ^<type> <number>)
   (<lo> ^types 1
         ^type <type>
         ^number <number>)
-->
   (<s> ^operator <o> < )}

sp {mac*select*operator*avoid*inverse*not*failure*2
   (state <s> ^name mac
              ^operator <o> +
             -^failure <d>
              ^last-operator.types 2)
   (<o> ^types 2)
-->
   (<s> ^operator <o> < )}

### ABSTRACT.
### This file provides a Soar system to solve the missionaries and
### cannibals problem.
###
### PROBLEM STATEMENT
### Three missionaries and three cannibals come to a river. There is a a
### boat on their bank of the river that can be used by either one or
### two persons at a time.  This boat must be used to cross the river in
### such a way that cannibals never outnumber missionaries on either
### bank of the river.
###

### DESCRIPTION.
### Simple state representation where the state has two objects: one for
### each bank of the river. Each of these has augmentations for
### missionaries, cannibals, and the boat; with their values being the
### number of the entity type on that bank of the river. This is the
### version covered in Soar 8 Tutorial.  Copying the states is more
### complex (two-level-attributes) than other versions, but initial
### state definition, operator proposal and application are simplier.
###

sp {mac*propose*initialize
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> + =)
   (<o> ^name initialize-mac)
}

sp {mac*apply*initialize-io
   (state <s> ^operator.name initialize-mac)
-->
   (<s> ^name mac
        ^desired <d>)
   (<d> ^right-bank <dr>)
   (<dr> ^missionaries 3
         ^cannibals 3
	 ^boat 1)}

###
# New elaborations to copy state
###
sp {mac*elaborate*state*left-bank
   (state <s> ^name mac
              ^io.input-link.left-bank <lb>)
-->
   (<s> ^left-bank <lb>)}

sp {mac*elaborate*state*right-bank
   (state <s> ^name mac
              ^io.input-link.right-bank <rb>)
-->
   (<s> ^right-bank <rb>)}

###
#sp {mac*apply*initialize
#   (state <s> ^operator.name initialize-mac)
#-->
#   (<s> ^name mac
#        ^left-bank <l>
#        ^right-bank <r>
#        ^desired <d>)
#   (<l> ^missionaries 3
#        ^cannibals 3
#        ^boat 1
#        ^other-bank <r>)
#   (<r> ^missionaries 0
#        ^cannibals 0
#        ^boat 0
#        ^other-bank <l>)
#   (<d> ^right-bank <dl>)
#   (<dl> ^missionaries 3
#         ^cannibals 3
#         ^boat 1)
#}

sp {mac*propose*operator*move-boat1
   "Moves either a single missionary or a cannibal."
   (state <s> ^name mac
              ^<< right-bank left-bank >> <bank>)
   (<bank> ^{ << cannibals missionaries >> <type> } > 0
           ^boat 1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move-boat
        ^bank <bank>
        ^<type> 1
        ^boat 1
        ^types 1)}

sp {mac*propose*operator*move-boat2
   "Moves two missionaries or two cannibals."
   (state <s> ^name mac
              ^ << right-bank left-bank >> <bank>)
   (<bank> ^{ << cannibals missionaries >> <type> } > 1
           ^boat 1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move-boat
        ^bank <bank>
        ^<type> 2
        ^boat 1
        ^types 1)}

sp {mac*propose*operator*move-boat11
   (state <s> ^name mac
              ^ << right-bank left-bank >> <bank>)
   (<bank> ^missionaries > 0
           ^cannibals > 0
           ^boat 1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name move-boat
        ^bank <bank>
        ^missionaries 1
        ^cannibals 1
        ^boat 1
        ^types 2)}

###
### move-boat OPERATOR IMPLEMENTATION
###

#sp {apply*move-boat
#   (state <s> ^operator <o>)
#   (<o> ^name move-boat
#        ^{ << missionaries cannibals boat >> <type> } <num>
#        ^bank <bank>)
#   (<bank> ^<type> <bank-num>
#           ^other-bank <obank>)
#   (<obank> ^<type> <obank-num>)
#-->
#   (<bank> ^<type> <bank-num> -
#                   (- <bank-num> <num>))
#   (<obank> ^<type> <obank-num> -
#                    (+ <obank-num> <num>))}

sp {apply*move-boat*add-ouput*1type
   (state <s> ^operator <o>
              ^io.output-link <out>)
   (<o> ^name move-boat
        ^{ << missionaries cannibals >> <type> } <num>
        ^boat 1
        ^types 1
	^bank <bank>)
-->
   (<out> ^move-boat <mb>)
   (<mb> ^<type> <num>
         ^boat 1
         ^from-bank <bank>)}

sp {apply*move-boat*add-ouput*2types
   (state <s> ^operator <o>
              ^io.output-link <out>)
   (<o> ^name move-boat
        ^missionaries <m-num>
        ^cannibals <c-num>
        ^boat 1
        ^types 2
	^bank <bank>)
-->
   (<out> ^move-boat <mb>)
   (<mb> ^missionaries <m-num>
         ^cannibals <c-num>
         ^boat 1
         ^from-bank <bank>)}

sp {apply*move-boat*remove-output
   (state <s> ^operator <o>
              ^io.output-link <out>)
   (<out> ^move-boat <mb>)
   (<mb> ^status complete)
-->
   (<out> ^move-boat <mb> -)}

###
### SEARCH CONTROL
###


sp {mac*apply*move-boat*record*last-operator*types*1
   (state <s> ^name mac
              ^operator <o>)
   (<o> ^name move-boat
        ^bank <bank>
        ^{ << missionaries cannibals >> <type> } <n>
        ^types 1)
   -->
   (<s> ^last-operator <o1>)
   (<o1> ^types 1
         ^bank <bank>
         ^type <type>
         ^number <n>)}

sp {mac*apply*move-boat*record*last-operator*types*2
   (state <s> ^name mac
              ^operator <o>)
   (<o> ^name move-boat
        ^boat <bank>
        ^types 2)
   -->
   (<s> ^last-operator <o1>)
   (<o1> ^types 2
         ^bank <bank>)}

sp {mac*apply*move-boat*remove*old*last-operator
   (state <s> ^name mac
              ^operator <o>
              ^<lr-bank>.other-bank <o-bank>
              ^last-operator <lo>)
   (<lo> ^bank <obank>)
   -->
   (<s> ^last-operator <lo> -)}

