## -*- Mode: sde -*-
##
############################# -*- Mode: Soar -*- #############################
## 
## File            : default/simple-selection.soar
## Author          : John Laird
## Created on      : Mon Feb 27 16:15:34 1989  
## Last Modified By: John Laird 
## Last Modified On: Friday December 11, 1998
## 
## Contents:     Default productions for simple-selection problem space for Soar 8.1
##               Only works for one level copies of internal states - copies
##                 every attribute on the top-level state unless the top-state
##                 has a world-state attribute, in which case it copies all of the
##                 world-state attributes.
##
###############################################################################

## Significant Changes made to Convert to Soar 8.1

###
###		iv.	Possible Problems
###
###	The default knowledge has a mind set that all evaluate-object operators
### will either be directly implemented, or will be implemented in a subspace
### that is performing look ahead style evaluation.  This should be checked to
### see if it causes errors.
###
###	There is still one opp-app declaration, due to a bug in support classification.
###
###	Evaluate-rejected and evaluate-primary are still in this knowledge as
### operator names, but are not proposed by this knowledge.
###
###		The selection space for multi-choice impasses
###

### Use the selection problem space for all choice multiple, impasses 
### - make it worst so that any other will dominate.

sp {default*selection*propose*space*selection
   :default
   (state <s> ^choices multiple)
   -->
   (<s> ^problem-space.name selection)}

### Signal that a value other than novalue has been assigned.

sp {default*selection*elaborate*state*found-value-true
   :default
   (state <s> ^problem-space.name selection
	      ^choices multiple)
   (<s> ^evaluation <e>)
   (<e> ^ << numeric-value symbolic-value >> <> novalue)
   -->
   (<s> ^found-value true)}


### Signal that there are still objects to evaluate.

sp {default*selection*elaborate*state*wait-true-not-all-objects-evaluated
   :default
   (state <s> ^problem-space.name selection
	      ^operator <o> +
	      ^choices multiple)
   - {(<o> ^type evaluation
	   ^evaluation <e>)
     (<e> ^ << numeric-value symbolic-value >> )}
   -->
   (<s> ^wait true)}

###
###		7.3.1	The evaluate-object operator
###

### Create evaluate operator in selection problem space.

sp {default*selection*propose*evaluate-object
   :default
   (state <s> ^problem-space.name selection
	      ^item <x>)
   -{(state <s> ^evaluation <e>)	#"state" added. AA/mvp 9-2-94
    (<e> ^object <x>
	 ^value t)}
   -->
   (<o> ^state <s> 
        ^name evaluate-object 
        ^object <x>)
   (<s> ^operator <o> +)}


### Make them indifferent if not ^operator-selection not-indifferent.

sp {default*selection*select*evaluate-object*indifferent
   :default
   (state <s> ^problem-space <p>
	      ^operator <o> +)
   (<p> ^name selection
      - ^operator-selection not-indifferent)
   (<o> ^name evaluate-object)
   -->
   (<s> ^operator <o> =)}

### Label all evaluation operators with ^type evaluation.

sp {default*selection*elaborate*operator*evaluate-object*type-evaluation 
   :default
   (state <s> ^operator <o>)
   (<o> ^name << evaluate-object evaluate-dominated evaluate-primary >> )
   -->
   (<o> ^type evaluation)}    

### Create evaluation once the eval operator is selected.
 
sp {default*selection*elaborate*state*evaluation
   :default
   (state <s> ^operator <o>)
   (<o> ^type evaluation)
   -->
   (<s> ^evaluation <e>) 
   (<e> ^state <s> ^operator <o>)
   (<o> ^evaluation <e>)}

sp {default*selection*elaborate*evaluate-object*desired
   :default
   (state <s> ^operator <o>
	      ^superstate.desired <d>)
   (<o> ^type evaluation)
   -->
   (<o> ^desired <d>)}

sp {default*selection*elaborate*evaluate-object*superproblem-space
   :default
   (state <s> ^problem-space.name selection
	      ^operator <o>
	      ^superstate.problem-space <p2>)
   (<o> ^type evaluation)
   -->
   (<o> ^superproblem-space <p2>)}

sp {default*selection*elaborate*evaluate-object*superstate
   :default
   (state <s> ^problem-space.name selection
	      ^operator <o>
	      ^superstate <s2>)
   (<o> ^type evaluation)
   -->
   (<o> ^superstate <s2>)}

### If all objects have been evaluated, and all have value novalue, and
### there is a object being evaluated in a higher context, assign that
### object novalue.

sp {default*selection*elaboare*evaluate-object*all-objects-novalue
   :default
   (state <s> ^superstate.superstate <sss>
	      ^operator <o>
	    - ^wait
	    - ^found-value
	      ^quiescence t
	      ^choices multiple)
   (state <sss> ^problem-space.name selection
		^operator <sso>)
   (<sso> ^type evaluation)
   -->
   (<sso> ^numeric-value novalue ^evaluation-type final)}


###
###		7.3.1.1 Evaluation Objects
###

sp {default*selection*elaborate*evaluation*value
   :default
   (state <s> ^evaluation <e>)
   (<e> ^ << numeric-value symbolic-value >>)
   -->
   (<e> ^value t)}

sp {default*selection*elaborate*evaluation*desired
   :default
   (state <s> ^superstate.desired <d>
	      ^evaluation <e>)
   -->
   (<e> ^desired <d>)}

sp {default*selection*elaborate*evaluation*object
   :default
   (state <s> ^operator <o>
	      ^evaluation <e>)
   (<o> ^object <x>
	^evaluation <e>
	^type evaluation)
   -->
   (<e> ^object <x>)}

sp {default*selection*elaborate*evaluation*type
   :default
   (state <s> ^operator <o>
	      ^evaluation <e>)
   (<o> ^name <name>
	^evaluation <e>
	^type evaluation)
   -->
   (<e> ^type <name>)}


###
###		7.3.1.2 Applying the evaluate-object operator
###
###	Domain specific code may directly implement an evaluation, or an operator no-change may occur
### to do the job, see the next section.



###
###		7.3.2   The evaluation subgoal
###

###
### These productions which fire once an evaluation subgoal has been created
### have also been generalized.

### SBH/mvp 9-2-94
sp {default*evaluate-object*elaborate*state*desired
   :default
   (state <s> ^impasse no-change
	      ^attribute operator 
              ^superstate.operator <so>)
   (<so> -^default-desired-copy yes ^type evaluation ^desired <d>)
   -->
   (<s> ^desired <d>)}


sp {default*evaluate-object*elaborate*state*name
   :default
   (state <s> ^impasse no-change 
              ^attribute operator 
              ^superstate.operator.name evaluate-object)
   -->
   (<s> ^name implement-evaluate-object)}

sp {default*evaluate-object*elaborate*state*problem-space
   :default
   (state <s> ^impasse no-change
	      ^attribute operator
	      ^superstate.operator <so>)
   (<so> ^type evaluation
	 ^superproblem-space <p>)
   -->
   (<s> ^problem-space <p>)}

## install a copy of the state, when ^default-state-copying and there
## is an operator tie.

sp {default*evaluate-object*elaborate*state*duplicate-of
   :default
   (state <s> ^problem-space <p>
	      ^superstate <ss>
	      ^default-state-copy yes)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation
	 ^superstate <sss>) 
   -->
   (<s> ^duplicate-of* <sss>)}

### Added test for superproblem-space.  This restricts these productions
### to only fire in a lookahead.  Previously, these productions made it
### impossible to use evaluation problem spaces other than lookahead.
### JEL 6/21/91

sp {default*evaluate-object*elaborate*state*look-ahead-operator*copy
   :default
   (state <s> ^problem-space.name <n> ^superstate.operator <so>)
   (<so> ^type evaluation 
	 ^default-operator-copy yes ^object <o> ^superproblem-space.name <n>)
   -->
   (<s> ^look-ahead-operator <copy-o>)
   (<copy-o> ^duplicate-of* <o>)}   


## Create pointer for uniform access even in no copy

### Added test for superproblem-space.  This restricts these productions
### to only fire in a lookahead.  Previously, these productions made it
### impossible to use evaluation problem spaces other than lookahead.
### JEL 6/21/91

sp {default*evaluate-object*elaborate*state*look-ahead-operator*no-copy
   :default
   (state <s> ^problem-space <p> ^superstate.operator <so>)
   (<so> ^type evaluation 
	 ^default-operator-copy no ^object <o> ^superproblem-space <p>)
   -->
   (<s> ^look-ahead-operator <o>)}   

### install copy of operator in lookahead context.
### <p> / <ssp> checks to correct bug from TJ
### Modified by JEL/mvp 07-Nov-93

sp {default*evaluate-object*propose*look-ahead
   :default
   (state <s> ^problem-space.name <n> ^look-ahead-operator <o> ^superstate <ss>)
   (state <ss> ^superstate.problem-space.name <n> ^operator.type evaluation)
  -(<s> ^tried-tied-operator <o>)
   -->
   (<s> ^operator <o> ! +)}


### Detect that the operator has been tried when a duplicate is being used.

sp {default*evaluate-object*elaborate*state*tried-tied-operator*copy
   :default
   (state <s> ^problem-space <p> ^operator <o-copy> 
	      ^superstate <ss> ^default-operator-copy yes)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation 
	 ^superproblem-space <p> ^object <o>)
   (<o-copy> ^duplicate-of* <o>)
   -->
   (<s> ^tried-tied-operator <o-copy>)}

## Detect that the operator has been tried when a duplicate is not being used.

sp {default*evaluate-object*elaborate*state*tried-tied-operator*no-copy
   :o-support
   :default
   (state <s> ^problem-space <p> ^operator <o>
	      ^superstate <ss> ^default-operator-copy no)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation
	 ^superproblem-space <p> ^object <o>)
   -->
   (<s> ^tried-tied-operator <o>)}


### Give symbol-value failure when no operators are proposed in subgoals

sp {default*evaluate-object*elaborate*evaluation*failure*no-operators
   :default
   (state <select-g> ^operator <o2>)
   (<select-g> ^evaluation <e2>)
   (<o2> ^type evaluation ^evaluation <e2>)
   (state <eval-g> ^superstate <select-g> ^quiescence t)
   (state <state-nc> ^superstate <eval-g> ^attribute state ^impasse no-change ^quiescence t)
   (state <goal-nc> ^superstate <state-nc> ^attribute state ^impasse no-change ^quiescence t)
   -->
   (<e2> ^symbolic-value failure)}


sp {default*evaluate-object*elaborate*evaluation*failure*constraint-failure
   :default
   (state <select-g> ^operator <o2>)
   (<select-g> ^evaluation <e2>)
   (<o2> ^type evaluation ^evaluation <e2>)
   (state <eval-g> ^superstate <select-g>)
   (state <rej-state-imp> ^superstate <eval-g> ^attribute operator
			  ^impasse constraint-failure)
   -->
   (<e2> ^symbolic-value failure)}

sp {default*evaluate-object*elaborate*evaluation*from-subgoal
   :default
   (state <s> ^superstate <ss>
	      ^ { << required-success success partial-success 
		indifferent partial-failure failure prohibit-failure
		draw lose win >> <svalue> } <eb>)
   (state <ss> ^operator <so> ^evaluation <e>)
   (<so> ^type evaluation ^evaluation <e> ^desired <eb>)
   -->
   (<e> ^symbolic-value <svalue>)}


##                                Added to handle duplicated desireds
sp {default*evaluate-object*elaborate*evaluation*from-subgoal*duplicate-desired
   :default
   (state <s> ^superstate <ss>
	      ^ { << required-success success partial-success 
		indifferent partial-failure failure prohibit-failure
		draw lose win >> <svalue> } <deb>)
   (state <ss> ^operator <so>
	       ^evaluation <e>)
   (<so> ^type evaluation ^evaluation <e> ^desired <eb>)
   (<deb> ^duplicate-of* <eb>)
   -->
   (<e> ^symbolic-value <svalue>)}


###
###		7.3.2.1.	State Copying
###

## If no signals on problem space, the default is to copy both state and operator

sp {default*evaluate-object*elaborate*operator*type-evaluation*default-for-default-state-copy-is-yes 
   :default
   (state <ss>^operator <so>)
   (<so> ^type evaluation 
	 ^superproblem-space <p>)
  -(<p> ^default-state-copy no)
   -->
   (<so> ^default-state-copy yes)}

sp {default*evaluate-object*elaborate*operator*type-evaluation*default-state-copy-from-problem-space 
   :default
   (state <ss> ^operator <so>)
   (<so> ^type evaluation 
	 ^superproblem-space <p>)
   (<p> ^default-state-copy <yes-or-no>)
   -->
   (<so> ^default-state-copy <yes-or-no>)}

### Default is that all-attributes-at-level are copied

sp {default*evaluate-object*elaborate*state*default-state-copy-from-type-evaluation-operator 
   :default
   (state <s> ^impasse no-change ^attribute operator ^superstate <ss>)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation ^default-state-copy <yes-no>)
   -->
   (<s> ^default-state-copy <yes-no>)}

sp {default*evaluate-object*elaborate*state*default-copy-is-all-attributes-at-level-one 
   :default
   (state <s> ^impasse no-change ^attribute operator ^superstate <ss>)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation 
	 ^superproblem-space <p>)
   (<p> -^all-attributes-at-level two
       -^one-level-attributes
       -^two-level-attributes
       -^dont-copy
       -^dont-copy-anything)
   -->
   (<s> ^all-attributes-at-level one)}

### We want ^all-attributes-at-level one to be on the goal, but if someone
### puts it on the space along with the other copying flags, we should allow
### that.  So, copy it from the space to the goal.

sp {default*evaluate-object*elaborate*state*copy-all-attributes-at-level-one-from-problem-space 
   :default
   (state <s> ^problem-space <p> ^default-state-copy yes)
   (<p> ^all-attributes-at-level one)
   -->
   (<s> ^all-attributes-at-level one)}

### Set up identifier mapping table for state duplication. 

sp {default*evaluate-object*elaborate*state*create-duplicates-table 
   :default
   (state <s> ^impasse no-change ^attribute operator ^superstate <ss>)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation
         ^default-state-copy yes)
   -->
   (<s> ^duplicates* <d>)}

### Copy one-level-attributes.

### Replaced test for implement-evaluate-object with test for superproblem-
### space. Makes the productions more general and is consistent with other
### default productions for lookahead search.  JEL 6/21/91 

sp {default*evaluate-object*elaborate*state*add-one-level-attributes 
   :default
   (state <ss> ^operator <so>)
   (<so> ^superproblem-space <p>)
   (state <s> ^problem-space <p> ^superstate <ss>
	      ^default-state-copy yes)  
   (<p> ^one-level-attributes <att>)
   (<s> ^duplicate-of* <ds>)
   (<ds> ^<att> <val>)
   -->
   (<s> ^<att> <val>)}

## Copy all attributes, when ^all-attributes-at-level one.

### Replaced test for implement-evaluate-object with test for superproblem-
### space. Makes the productions more general and is consistent with other
### default productions for lookahead search.  JEL 6/21/91 

sp {default*evaluate-object*elaborate*state*add-all-attributes-at-level-one 
   :default
   (state <sg> ^operator <so>)
   (<so> ^superproblem-space <p>)
   (state <s> ^problem-space <p> ^superstate <sg>
	      ^default-state-copy yes ^all-attributes-at-level one
	      ^duplicate-of* <ss>)
   (<p> - ^dont-copy-anything - ^dont-copy <att>
      - ^two-level-attributes <att>)
   (<ss> ^{ <> duplicate-of* <> tried-tied-operator <> problem-space 
            <> impasse <> attribute <> choices <> quiescence 
            <> operator <> superstate 
            <att> } <val>)
   -->
   (<s> ^<att> <val>)}

### If we copied a one-level-attribute, and it later turned
### out that its value happened to be something that was pointed to by a
### two-level-attribute, change its value to be the copied object.

sp {default*evaluate-object*elaborate*state*change-one-level-attribute-to-duplicate 
   :default
   (state <s> ^problem-space <p>
	      ^duplicates* <d>
	      ^default-state-copy yes
	      ^duplicate-of* <ss>)
   (<p> ^one-level-attributes <att>)
   (<ss> ^<att> <id>)
   (<d> ^<id> <new-id>)
   -->
   (<s> ^<att> <id> - <new-id>)}

### Fixes, when all-attributess-at-level one.

sp {default*evaluate-object*elaborate*state*change-all-attributes-at-level-one-to-duplicate
   :default
   (state <s> ^problem-space <p>
	      ^duplicates* <d>
	      ^default-state-copy yes
	      ^all-attributes-at-level one
	      ^duplicate-of* <ss>)
   (<p> - ^dont-copy <att>)
   (<ss> ^{ <> tried-tied-operator <att> } <id>)
   (<d> ^<id> <new-id>)
   -->
   (<s> ^<att> <id> - <new-id>)}

### Create new ids for two-level-attributes.

sp {default*evaluate-object*elaborate*state*duplicate-id-for-attribute 
   :default
   (state <s> ^problem-space <p>
	      ^duplicates* <d> ^default-state-copy yes
	      ^duplicate-of* <ss>)
   (<p> ^two-level-attributes <att>)
   (<ss> ^<att> <id>)
   -->
   (<d> ^<id> <new-id> + =)}

### Create new ids for all attributes, when ^all-attributes-at-level two.

sp {default*evaluate-object*elaborate*state*duplicate-id-for-all-attributes 
   :default
   (state <s> ^problem-space <p>
	      ^duplicates* <d>
	      ^default-state-copy yes
	      ^duplicate-of* <ss>)
   (<p> -^dont-copy-anything
       -^dont-copy <att>
	^all-attributes-at-level two
       -^one-level-attributes <att>)
   (<ss> ^{ <> duplicate-of* <> tried-tied-operator <> problem-space 
            <> impasse <> attribute <> choices <> quiescence
            <> operator <> superstate 
            <att> } <id>)
   -->
   (<d> ^<id> <new-id>)}

### Create a copy of the old object on the new link.  First copy
### augmentations that don't point to objects that have been
### duplicated. 

#set warnings off
warnings --off
sp {default*evaluate-object*elaborate*add-attribute-to-duplicate 
   :default
   (state <s> ^duplicates* <d> 
	      ^default-state-copy yes)
   (<d> ^<id> <new-id>
      - ^<sub-val>)
   (<id> ^ { <> tried-tied-operator <sub-att> } <sub-val>)
   -->
   (<new-id> ^<sub-att> <sub-val>)}
#set warnings on
warnings --on

### Next copy augmentations that point to duplicated objects:

#set warnings off
warnings --off
sp {default*evaluate-object*elaborate*add-duplicated-attribute-to-duplicate 
   :default
   (state <s>
	^duplicates* <d> ^default-state-copy yes)
   (<d> ^<id> <new-id>
	^<sub-val> <new-val>)
   (<id> ^ { <> tried-tied-operator <sub-att> } <sub-val>)
   -->
   (<new-id> ^<sub-att> <new-val>)}
#set warnings on
warnings --on

### Added ^two-level-attributes <att>.  Previously, it
### would copy any attribute that happened to point to the same value as
### one of the two-level-attributes.

sp {default*evaluate-object*elaborate*state*add-duplicate-to-state 
   :default
   (state <s> ^problem-space <p>
	      ^duplicates* <d> ^default-state-copy yes
	      ^duplicate-of* <ss>)
   (<p> ^two-level-attributes <att>)
   (<ss> ^<att> <id>)
   (<d> ^<id> <new-id>)
   -->
   (<s> ^<att> <new-id>)}


### moves the new links to the state when ^all-atts two.

sp {default*evaluate-object*elaborate*state*add-duplicates-for-all-attributes 
   :default
   (state <s> ^problem-space <p>
	      ^duplicates* <d>
	      ^default-state-copy yes
	      ^duplicate-of* <ss>)
   (<p> ^all-attributes-at-level two
      - ^dont-copy-anything
      - ^dont-copy <att>
      - ^one-level-attributes <att>)
   (<ss> ^{ <> tried-tied-operator <att> } <id>)
   (<d> ^<id> <new-id>)
   -->
   (<s> ^<att> <new-id>)}

## -------------------------------
## Desired copying:
## -------------------------------

## Copy attributes to the duplicate desired state.  We
## do this after the state is installed so that the duplicates table
## will already have been filled in.  We just copy all attributes over
## from the original operator, except that attributes that pointed to 
## objects that have been duplicated (on the state) are modified to point
## to the duplicate objects.

sp {default*evaluate-object*elaborate*state*eval*desired 
   :default
   (state <s> ^superstate <ss>)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation ^desired <d> ^default-desired-copy yes)
   -->
   (<s> ^desired <d-copy>)
   (<d-copy> ^duplicate-of* <d>)}   


## Copy attributes whose values haven't been duplicated.

### SBH/mvp 9-2-94
### Added parallel preference to the copied attribute.
### Also, added the condition that the desired be attached to
### an evaluation superoperator.  This is because if we use the same 
### desired in a lower lookahead context, this production re-firing 
### can cause stupid chunks. 

sp {default*duplicate-desired*copy-old-value
   :default
   (state <g> ^superstate <sg> ^duplicates* <d> ^desired <o-copy>)
   (state <sg> ^operator <so>)
   (<so> ^type evaluation ^desired <o>)
   (<o-copy> ^duplicate-of* <o>)
   (<o> ^{ <> duplicate-of* <att> } <val> -^dont-copy <att>)
 - (<d> ^<val>)
 -->
   (<o-copy> ^<att> <val>)}

## Copy attributes whose values have been duplicated.

sp {default*duplicate-desired*replace-old-value
   :default
   (state <s> ^duplicates* <d> ^desired <o-copy>)
   (<o-copy> ^duplicate-of* <o>)
   (<o> ^ { <> duplicate-of* <att> } <id>
      - ^dont-copy <att>)
   (<d> ^<id> <copy-id>)
   -->
   (<o-copy> ^<att> <copy-id>)}



###		7.3.2.3 Operator copying	
###
### Copy attributes to the duplicate operator.  We
### do this after the state is installed so that the duplicates table
### will already have been filled in.  We just copy all attributes over
### from the original operator, except that attributes that pointed to 
### objects that have been duplicated (on the state) are modified to point
### to the duplicate objects.


sp {default*evaluate-object*elaborate*operator*default-operator-copy-is-yes 
   :default
   (state <ss> ^operator <so>)
   (<so> ^type evaluation 
	 ^superproblem-space <p>)
  -(<p> ^default-operator-copy no)
   -->
   (<so> ^default-operator-copy yes)}


sp {default*evaluate-object*elaborate*operator*copy-default-operator-copy-from-problem-space 
   :default
   (state <ss> ^operator <so>)
   (<so> ^type evaluation 
	 ^superproblem-space <p>)
   (<p> ^default-operator-copy <yes-or-no>)
   -->
   (<so> ^default-operator-copy <yes-or-no>)}


sp {default*evaluate-object*elaborate*state*copy-default-operator-copy-from-type-evaluation-operator 
   :default
   (state <s> ^impasse no-change ^attribute operator ^superstate <ss>)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation ^default-operator-copy <yes-no>)
   -->
   (<s> ^default-operator-copy <yes-no>)}

### Set up identifier mapping table for operator

sp {default*evaluate-object*elaborate*state*create-duplicates-table-for-operator-only 
   :default
   (state <s> ^impasse no-change ^attribute operator ^superstate <ss>)
   (state <ss> ^operator <so>)
   (<so> ^type evaluation
         ^default-operator-copy yes 
       - ^default-state-copy yes)
   -->
   (<s> ^duplicates* <d>)}

### Copy attributes whose values haven't been duplicated.
###

sp {default*evaluate-object*elaborate*operator*add-attribute-to-duplicate-operator 
   :default
   (state <s> ^duplicates* <d> ^look-ahead-operator <o-copy>)
   (<o-copy> ^duplicate-of* <o>)
   (<o> ^ { <> duplicate-of* <att> } <val>
      - ^ dont-copy <att>)
   (<d> - ^<val>)
   -->
   (<o-copy> ^<att> <val>)}

### Copy attributes whose values have been duplicated.

sp {default*evaluate-object*elaborate*operator*add-duplicated-attribute-to-duplicate-operator 
   :default
   (state <s> ^duplicates* <d> ^look-ahead-operator <o-copy>)
   (<o-copy> ^duplicate-of* <o>)
   (<o> ^ { <> duplicate-of* <att> } <id>
      - ^dont-copy <att>)
   (<d> ^<id> <copy-id>)
   -->
   (<o-copy> ^<att> <copy-id>)}


###
###     7.3.3   Computing evaluations
###
###
###     7.3.3.1 Computing numeric evaluations
###
### This is domain specific, so there are no default productions for this.

###
###     7.3.3.2 Comparing numeric evaluations
###

### If two objects have equal evaluations they are indifferent.

sp {default*selection*compare*equal-evaluation-indifferent 
   :default
   (state <s> ^problem-space.name selection #^attribute <role>  #REMOVED.
              ^superstate <ss>
              ^evaluation <e1>  { <> <e1> <e2> })
   (<ss> ^desired <d>)
   (<e1> ^object <x> ^numeric-value <v> ^desired <d> 
	 ^type evaluate-object)
   (<e2> ^object <y> ^numeric-value <v> ^desired <d> 
	 ^type evaluate-object)
 - (<d> ^equal not-indifferent)
   -->
   (<ss> ^operator <x> = <y>)}	# was ^<role> 


###
### Generate operator preferences based on their evaluations and info 
### as to whether higher or lower evaluations are better.

sp {default*selection*compare*higher-evaluation-better 
   :default
   (state <s> ^problem-space.name selection 
              ^superstate <ss>
	      ^evaluation <e1> ^evaluation { <> <e1> <e2> })
   (state <ss> ^problem-space <p2> ^desired <d>)
   (<e1> ^object <o1> ^numeric-value <v> ^desired <d>
	 ^type evaluate-object)
   (<e2> ^object <o2> ^numeric-value < <v> ^desired <d> 
	 ^type evaluate-object)
   (<d> ^better higher)
   -->
   (<ss> ^operator <o2> < <o1>)}
   # was ^<role> 

sp {default*selection*compare*prefer-lower-evaluation 
   :default
   (state <s> ^problem-space.name selection #^attribute <role>   #REMOVED. 
              ^superstate <ss>
	      ^evaluation <e1> ^evaluation { <> <e1> <e2> })
   (state <ss> ^problem-space <p2> ^desired <d>)
   (<e1> ^object <o1> ^numeric-value <v> ^desired <d> 
	 ^type evaluate-object)
   (<e2> ^object <o2> ^numeric-value > <v> ^desired <d> 
	 ^type evaluate-object)
   (<d> ^better lower)
   -->
   (<ss> ^operator <o2> < <o1>)}  


###
###		7.3.3.3 Computing symbolic evaluations
###


sp {default*selection*compare*same-symbolic-evaluations-are-indifferent
   :default
   (state <s> ^problem-space.name selection ^superstate <ss>
	      ^evaluation <e1>  { <> <e1> <e2> })
   (state <ss> ^desired <d>)
   (<e1> ^object <x> ^symbolic-value <v> ^desired <d> 
	 ^type evaluate-object)
   (<e2> ^object <y> ^symbolic-value <v> ^desired <d> 
	 ^type evaluate-object)
   -->
   (<ss> ^operator <x> = <y>)}	

sp {default*selection*compare*success-evaluation-better-than-partial-success
   :default
   (state <s> ^problem-space.name selection ^superstate <ss>
              ^evaluation <e1>  { <> <e1> <e2> })
   (state <ss> ^desired <d>)
   (<e1> ^object <x> ^symbolic-value success ^desired <d> 
         ^type evaluate-object)
   (<e2> ^object { <> <x> <y> } ^symbolic-value partial-success 
         ^desired <d> ^type evaluate-object)
   -->
   (<ss> ^operator <x> > <y>)}	

sp {default*selection*compare*partial-failure-evaluation-better-than-failure
   :default
   (state <s> ^problem-space.name selection ^superstate <ss>
              ^evaluation <e1>  { <> <e1> <e2> })
   (state <ss> ^desired <d>)
   (<e1> ^object <x> ^symbolic-value partial-failure ^desired <d> 
         ^type evaluate-object)
   (<e2> ^object { <> <x> <y> } ^symbolic-value failure ^desired <d>
         ^type evaluate-object)
   -->
   (<ss> ^operator <x> > <y>)}	

sp {default*selection*select*required-success-evaluation-becomes-required-preference
   :default
   (state <s> ^problem-space.name selection ^operator <o> ^superstate <ss>
	      ^evaluation <e1>)
   (state <ss> ^desired <eb> ^operator <o1> +)
   (<o> ^name evaluate-object ^evaluation <e1>
	^desired <eb> ^object <o1>)
   (<e1> ^symbolic-value required-success)
   -->
   (<ss> ^operator <o1> !)}

sp {default*selection*select*success-evaluation-becomes-best-preference
   :default
   (state <s> ^problem-space.name selection ^operator <o> ^superstate <ss>
              ^evaluation <e1>)
   (state <ss> ^desired <eb> ^operator <o1> +)
   (<o> ^name evaluate-object ^evaluation <e1>
	^desired <eb> ^object <o1>)
   (<e1> ^symbolic-value << partial-success success >>)
   -->
   (<ss> ^operator <o1> >)}

sp {default*selection*select*indifferent-evaluation-becomes-indifferent-preference
   :default
   (state <s> ^problem-space.name selection ^operator <o> ^superstate <ss>
	      ^evaluation <e1>)
   (state <ss> ^desired <eb> ^operator <o1> +)
   (<o> ^name evaluate-object ^evaluation <e1>
	^desired <eb> ^object <o1>)
   (<e1> ^symbolic-value indifferent)
   -->
   (<ss> ^operator <o1> =)}

sp {default*selection*select*partial-failure-evaluation-becomes-worst-preference
   :default
   (state <s> ^problem-space.name selection ^operator <o> ^superstate <ss>
              ^evaluation <e1>)
   (state <ss> ^desired <e> ^operator <o1> +)
   (<o> ^name evaluate-object ^evaluation <e1> 
	^desired <e>
        ^object <o1>)
   (<e1> ^symbolic-value partial-failure)
   -->
   (<ss> ^operator <o1> <)}

sp {default*selection*select*failure-evaluation-becomes-reject-preference
   :default
   (state <s> ^problem-space.name selection ^operator <o> ^superstate <ss>
	      ^evaluation <e1>)
   (state <ss> ^desired <e> ^operator <o1> +)
   (<o> ^name evaluate-object ^evaluation <e1> ^desired <e>
	^object <o1>)
   (<e1> ^symbolic-value << lose failure >>)
   -->
   (<ss> ^operator <o1> -)}

sp {default*selection*select*prohibit-failure-evaluation-becomes-prohibit-preference
   :default
   (state <s> ^problem-space.name selection ^operator <o> ^superstate <ss>
              ^evaluation <e1>)
   (state <ss> ^desired <e> ^operator <o1> +)
   (<o> ^name evaluate-object ^evaluation <e1> ^desired <e>
	^object <o1>)
   (<e1> ^symbolic-value prohibit-failure)
   -->
   (<ss> ^operator <o1> ~)}



###
###		7.3.3.4 Default evaluations
###

###			A.	default*pass-back-success
## if an operator leads to success and it is being
## tried out in a subgoal to evaluate another operator -
## give that second operator a success evaluation also


### AA/mvp 9-2-94
sp {default*pass-back-success 	
   :default
   (state <s> ^operator <o> ^superstate <ss>)	# removed ^evaluation <e1>
   (<o> ^name evaluate-object ^evaluation <e1>)
   (<e1> ^symbolic-value << required-success success >>)
   (state <ss> ^superstate <sss>)
   (state <sss> ^operator <sso>)	# removed ^evaluation <e2>
   (<sso> ^name evaluate-object ^evaluation <e2> ^object <x>)
   (<x> ^name)
   -->
   (<e2> ^symbolic-value partial-success)}



###
###		7.3.3.5 Novalue evaluations
###

### Make novalue worse than any other value.

sp {default*selection*compare*novalue-evaluation-always-worse
   :default
   (state <s> ^problem-space.name selection #^attribute <role>   #REMOVED. 
              ^superstate <ss>
	      ^evaluation <e1> ^evaluation { <> <e1> <e2> })
   (state <ss> ^desired <d>)
   (<e1> ^object <o1> ^numeric-value novalue ^desired <d>
	 ^type evaluate-object)
   (<e2> ^object <o2> ^numeric-value <> novalue ^desired <d>
	 ^type evaluate-object)
   -->
   (<ss> ^operator <o2> > <o1>)}	# was ^<role>


###
###	     7.3.4   Halting Soar with success or failure
###

###			A.	default*top-goal*halt*state*success
### Handle state augmentations dealing with goal termination for the top-level goal.

sp {default*top-goal*halt*success
   :default
   (state <s> ^name <name>
	      ^desired <eb>
	      ^superstate nil
	      ^<< required-success success >> <eb>)
   -->
   (write (crlf)   <name>  | achieved |)
   (halt)}

###			B.	default*top-goal*halt*state*failure

sp {default*top-goal*halt*failure
   :default
   (state <s> ^superstate nil
	      ^desired <eb>
	      ^name <name>
	      ^ << prohibit-failure failure >> <eb>)
   -->
   (write (crlf)  <name>  | failed|)
   (halt)}

###
###		7.5     Execution monitoring
###

### If the state is marked with ^success <d>, the goal succeeds.

sp {default*monitor*goal*success
   :default
   (state <ts> ^superstate nil - ^verbose false)
   (state <s> ^name <gname> ^desired <d>
	      ^<< partial-success required-success success >> <d>)
   -->
   (write (crlf) |  Goal | <gname> | succeeded. |)}

### If the state is marked with ^failure <d>, the goal fails.

sp {default*monitor*goal*failure
   :default
   (state <ts> ^superstate nil - ^verbose false)
   (state <s> ^name <gname> ^desired <d>
	      ^<< partial-failure failure prohibit-failure >> <d>)
   -->
   (write (crlf) |  Goal | <gname> | failed. |)}

### Print the evaluation of any object that has a name (which doesn't
### get states).

sp {default*monitor*operator*evaluation
   :default
   (state <ts> ^superstate nil -^verbose false)
   (state <s> ^operator <o>)
   (<o> ^type evaluation ^object <obj> ^evaluation <e>)
   (<e> ^ << numeric-value symbolic-value >> <n>)
   (<obj> ^name <name>)
   -->
   (write (crlf) |  Evaluation of | <obj> | (|
					     <name> |) is | <n>)}




	


