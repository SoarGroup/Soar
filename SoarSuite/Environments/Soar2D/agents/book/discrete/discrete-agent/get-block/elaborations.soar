sp {get-block*elaborate*substate
   (state <s> ^name get-block
              ^superstate.operator <so>)
   (<so> ^name get-block
         ^block <block>)
-->
   (<s> ^block <block>)
}
sp {get-block*elaborate*aligned-col
   (state <s> ^name get-block
              ^block.position.col <c>
              ^io.input-link.self.position.col <c>)
-->
   (<s> ^aligned col)
}

sp {get-block*elaborate*aligned-row
   (state <s> ^name get-block
              ^block.position.row <r>
              ^io.input-link.self.position.row <r>)
-->
   (<s> ^aligned row)
}

sp {get-block*elaborate*aligned-rotate-right
   (state <s> ^name get-block
              ^aligned
              ^block.angle-off.yaw > 0.78 )
-->
   (<s> ^rotate right)
}

sp {get-block*elaborate*aligned-rotate-left
   (state <s> ^name get-block
              ^aligned
              ^block.angle-off.yaw < -0.78 )
-->
   (<s> ^rotate left)
}

sp {get-block*elaborate*blocked-rotate-right
   (state <s> ^name get-block
              ^io.input-link.self.blocked.forward true
              ^io.input-link.self.blocked.right false)
-->
   (<s> ^rotate right)
}

sp {get-block*elaborate*blocked-rotate-left
   (state <s> ^name get-block
              ^io.input-link.self.blocked.forward true
              ^io.input-link.self.blocked.left false)
-->
   (<s> ^rotate left)
}
