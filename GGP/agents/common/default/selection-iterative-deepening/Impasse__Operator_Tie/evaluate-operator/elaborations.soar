sp {evaluate-operator*elaborate*state
   (state <s> ^superstate.operator <o>)
   (<o> ^name evaluate-operator
        ^evaluation.current-evaluation-depth <ced>)
-->
   (<s> ^current-evaluation-depth <ced>)
}
#
#sp {evaluate-operator*elaborate*evaluate-operator*current-evaluation-depth*0
#   :default
#   (state <s> ^superstate.operator <o>
#              ^desired <d>
#              ^gs <g>
#              ^current-evaluation-depth <= 0)
#   (<s> ^elaborations.two-steps-toward)
#   (<g> ^location <l1> <l2>)
#   (<l1> ^p1 mummy ^p2 <x1> ^p3 <x2>)
#   (<l2> ^p1 explorer ^p2 <x3> ^p3 <x4>)
#   (<o> ^name evaluate-operator
#        ^evaluation <e>)
#-->
#   (write (crlf) |Max evaluation depth hit.|)
#   (<s> ^depth-exceeded <d>)}
#

sp {evaluate-operator*elaborate*state*tried-tied-operator
   :default
   (state <s> ^look-ahead-operator <o> 
              ^operator <o>
              ^move-finished)
-->
   (<s> ^tried-tied-operator <o> +)
}

sp {evaluate-operator*elaborate*symbolic-evaluation*from-subgoal*depth-exceeded
   :default
   (state <ss> ^superstate <s>
               ^depth-exceeded <eb>)
   (<s> ^operator <so>
        ^evaluation <e>
        ^unevaluated-operator <u>
        ^allow-one-evaluation <a>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^superoperator <u>
         ^evaluation.desired <eb>)
-->
   (<e> ^symbolic-value depth-exceeded)
   (<s> ^unevaluated-operator <u> -
        ^allow-one-evaluation <a> -)}

sp {evaluate-operator*elaborate*symbolic-evaluation*from-subgoal
   :default
   (state <ss> ^superstate <s>
               ^ { << required-success success partial-success
                   indifferent partial-failure failure prohibit-failure
                   >> <svalue> } <eb>)
   (<s> ^operator <so>
        ^evaluation <e>
        ^unevaluated-operator <u>
        ^allow-one-evaluation <a>)
   (<so> ^name evaluate-operator
         ^evaluation <e>
         ^superoperator <u>
         ^evaluation.desired <eb>)
-->
   (<e> ^symbolic-value <svalue>)
   (<s> ^unevaluated-operator <u> -
        ^allow-one-evaluation <a> -)}
