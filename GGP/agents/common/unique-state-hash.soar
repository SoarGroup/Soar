sp {create*visited*states*hash*top
	(state <s> ^top-selection true)
-->
	(<s> ^visited-states-hash <vsh>)
}

# This will fail if there is no bookkeeping state present, and will act odd if
# more than one exists
sp {apply*analyze-state*hash*current*gs
	(state <s> ^duplicate-of <s2>
				^gs <gs>
				^operator.name analyze-state #must do this after update-state completed
        ^top-state.facts.counter <cnt>
        ^top-state.arbitrary-gs-constants <ags>)
-->
   (<s> ^current-state-hash (cmd struct-to-string <gs> -e <cnt> -s <ags>))
}

# if there is a current-state-hash during update state, it must be old
# so remove it-- this happens when a tie is resolved (or there is no tie),
# multiple game operators happen in the same state
sp {apply*update-state*hash*remove-stale
  (state <s> ^name game
             ^operator.name update-state
             ^current-state-hash <csh>)
-->
   (<s> ^current-state-hash <csh> -)
}

##################
# Depth 0 states are the current border states, they must be new.
# If it has ever been recorded before, this path should be marked as visited
sp {create*depth-0-states*hash*top
	(state <s> ^top-selection true)
-->
	(<s> ^depth-0-states-hash <d0sh>)
}

sp {create*unique*state*symbol
	(state <s> ^type state)
-->
	(<s> ^state-id (make-constant-symbol <s>))
}

sp {propose*depth-0-states
	(state <s> ^current-state-hash <csh>
				^current-evaluation-depth 0
				^state-id <state-id>)
	(state <s2> ^depth-0-states-hash <d0sh>)
	(<d0sh> -^<csh>)
-->
	(<s> ^operator <o> + =)
	(<o> ^name record-terminal-state)
}

sp {propose*depth-0-states*meed
        (state <s> ^current-state-hash <csh>
            ^current-evaluation-depth  <remaining-steps>
            ^elaborations <e> 
			^state-id <state-id>)
		(<e> ^minimum-explorer-exit-distance <meed>
			-^minimum-explorer-exit-distance <= <remaining-steps>)
        (state <s2> ^depth-0-states-hash <d0sh>)
        (<d0sh> -^<csh>)
-->
        (<s> ^operator <o> + =)
        (<o> ^name record-terminal-state)
}


sp {prefer*record*terminal-state
	(state <s> ^operator <o1> + <o2> +)
	(<o1> ^name record-terminal-state)
	(<o2> ^name update-search-info)
-->
	(<s> ^operator <o1> > <o2>)
}

sp {apply*record*terminal-state
	(state <s> ^operator <o>
		^current-state-hash <csh>
		^state-id <state-id>)
	(<o> ^name record-terminal-state)
	(state <s2> ^depth-0-states-hash <d0sh>
             ^counter <val>)
-->
	(<d0sh> ^<csh> <state-id>) #record the state-id with it to avoid depth-exceed itself
   (<s2> ^counter <val> -)
   (<s2> ^counter (+ <val> 1))
}

sp {depth-0-depth-exceed
	(state <s> ^current-state-hash <csh>
				    ^current-evaluation-depth 0
				^state-id <state-id>
				^operator.name update-search-info)
	(<s> ^superstate.operator <so>
		^superstate.evaluation <e>)
	(<so> ^name evaluate-operator
		^superoperator <action-op>)
	(<e> ^superoperator <action-op>)
	(state <s2> ^depth-0-states-hash <d0sh>)
	(<d0sh> ^<csh> <state-id2> <> <state-id>)
-->
	(write (crlf) | State with depth 0 was visited previously, ID | <csh> )
	(<s> ^depth-exceeded <d>)
	(<e> ^visited-state true)
}

sp {depth-0-depth-exceed*meed
	(state <s> ^current-state-hash <csh>
				    ^current-evaluation-depth  <remaining-steps>
             ^elaborations <e> 
				^state-id <state-id>
				^operator.name update-search-info)
	(<e> ^minimum-explorer-exit-distance <meed>
			-^minimum-explorer-exit-distance <= <remaining-steps>)
	(<s> ^superstate.operator <so>
		^superstate.evaluation <e>)
	(<so> ^name evaluate-operator
		^superoperator <action-op>)
	(<e> ^superoperator <action-op>)
	(state <s2> ^depth-0-states-hash <d0sh>)
	(<d0sh> ^<csh> <state-id2> <> <state-id>)
-->
	(write (crlf) | State with depth 0 was visited previously, ID | <csh> )
	(<s> ^depth-exceeded <d>)
	(<e> ^visited-state true)
}

#if the same game state has been experienced before in substate, it means either it's a failure or it's in the circle - failure no doubt
# some visited states are 'failures' - hard to detect
# most are just exceed-depth type
sp {duplicate-state*exceed-depth
	(state <s> ^current-state-hash <csh>
				^desired <d>
				^operator.name check-for-duplicate-location
				^current-evaluation-depth <remaining-steps>)
	(state <ts> ^top-selection true
			^visited-states-hash <vsh>)
	(<vsh> ^<csh> <visited>)
	(<visited> ^remaining-steps >= <remaining-steps>)  #current must be depth-exceeded if the previously visited is depth-exceeded
	(<s> ^superstate.operator <so>
		^superstate.evaluation <e>)
	(<so> ^name evaluate-operator
		^superoperator <action-op>)
	(<e> ^superoperator <action-op>)
-->
	(write (crlf) | State was visited previously, ID | <csh> )
	(<s> ^depth-exceeded <d>)
	(<e> ^visited-state true)
}

sp {create*permanet*depth-exceeded*states*hash*top
	(state <s> ^top-selection true)
-->
	(<s> ^permanent-depth-exceeded-states-hash <pdesh>)
}

#fail the state permanetly if all its operator evaluations are failure or depth-exceeded due to visited state
sp {all*children*visited*state*alway*depth-exceeded
	(state <s> ^impasse tie
				^superstate.current-state-hash <csh>
				^current-evaluation-depth <remaining-steps>
				^remaining-operators 0)
	-{(<s> ^evaluation <e>)
		(<e> ^symbolic-value <> failure
			-^visited-state true)
	}
	(state <s2> ^permanent-depth-exceeded-states-hash <pdesh>)
-->
	(<pdesh> ^<csh> <h>)
	(<h> ^closure-steps <remaining-steps>)
	(write (crlf) |Found permenant depth-exceeded state | <csh> |, closure steps |<remaining-steps>(crlf))
}

#state is depth-exceeded regardless of remaining steps
sp {permanent-depth-exceeded-states*exceed-depth
	(state <s> ^current-state-hash <csh>
				^desired <d>
				^operator.name update-search-info)
	(state <ts> ^top-selection true
			^permanent-depth-exceeded-states-hash <pdesh>)
	(<pdesh> ^<csh> <h>)
	(<h> ^closure-steps <remaining-steps>)
	(<s> ^superstate.operator <so>
		^superstate.evaluation <e>)
	(<so> ^name evaluate-operator
		^superoperator <action-op>)
	(<e> ^superoperator <action-op>)
-->
   (write (crlf) | State is permanently depth-exceeded, ID | <csh> | closure steps |<remaining-steps>)
	(<s> ^depth-exceeded <d>)
	(<e> ^visited-state true)
}


sp {duplicate-state*update*repeats
	(state <s> ^current-state-hash <csh>
				^operator.name check-for-duplicate-location
				^current-evaluation-depth <remaining-steps>
				)
	(state <ts> ^top-selection true
				^visited-states-hash <vsh>
        ^counter <val>)
	(<vsh> ^<csh> <visited>)
	(<visited> ^remaining-steps >= <remaining-steps>  #current must be depth-exceeded if the previously visited is depth-exceeded
				^repeats <repeats>)
-->
	(<visited> ^repeats <repeats> -
				(+ <repeats> 1))
   (<ts> ^counter <val> -)
   (<ts> ^counter (+ <val> 1))
}

#if there is no operator tie, it won't be able to check duplicate state during the sequences of operators, but this is OK - it'll detect it in the end
# first time, init at 0
sp {copy*visited*state*to*top-selection*1
	(state <s> ^top-selection true
            ^counter <val>
				^visited-states-hash <vsh>)
	(state <s2> ^current-state-hash <csh> ^current-evaluation-depth <remaining-steps>
				^operator.name save-location
    ^superstate.name <name>)  #register visited state
	(<vsh> -^<csh>) #not previously visited state
-->
	(<vsh> ^<csh> <h>)
	(<h> ^remaining-steps <remaining-steps>
		^repeats 0)
   (<s> ^counter <val> -)
   (<s> ^counter (+ <val> 1))
   (write |New state saved: | (cmd struct-to-string <csh>))
}

# if existing visited state has lower remaining-depths, this is not depth-exceeded, update/increase remaining-steps to current one
sp {copy*visited*state*to*top-selection*2
	(state <s> ^top-selection true
            ^counter <val>
				^visited-states-hash <vsh>)
	(state <s2> ^current-state-hash <csh> ^current-evaluation-depth <remaining-steps>
				^operator.name save-location
    ^superstate.name <name>)  #register visited state
	(<vsh> ^<csh> <h>)
-->
	(<h> ^remaining-steps <remaining-steps>)
   (<s> ^counter <val> -)
   (<s> ^counter (+ <val> 1))
}

sp {assert*no-store-over-max
	(state <ts> ^top-selection true
        ^current-evaluation-depth <ced>
				^visited-states-hash <vsh>)
	(<vsh> ^<csh> <visited>)
	(<visited> ^remaining-steps > <ced>)  
-->
   (write (crlf) | ERROR: bad remaining-steps |)
   (halt)
}

sp {top-selection*counter*init
   (state <s> ^top-selection true)
-->
   (<s> ^counter 0)
}
