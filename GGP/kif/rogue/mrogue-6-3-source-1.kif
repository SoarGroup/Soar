;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; micro-Rogue (mRogue) GDL Rule Set
;;; Example: TL Level 6, Scenario 3, Source Task 1 
;;; See init/1 between ">>>>>" and "<<<<<" for unique aspects of this task
; David W. Aha (NRL)
; 2007 August 2
; 
; mRogue was developed by Tom Hinrichs, Sam Wintermute, & David Aha
;
; Summary: This source has armor and scrolls, while the other source
; has armor and potions. The target task has all of them.
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; This file: Map (and Legend)
;               + = Corner
;    012345     - = Horizontal wall
;   +------+    | = Vertical wall
;  0|......|    . = A room location
;  1|.a..s*|    # = A corridor location
;  2|@..B..|    @ = hero
;  3+----#-+    a = armor, p = potion, s = scroll,
;  4|..H!.s|    w = weapon, ! = amulet, * = gold
;  5|...s.>|    > = exit
;   +------+    K = kestral
;               H = hobgoblin
;               B = bat
;               S = snake
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Debugger: "http://games.stanford.edu:5999/director/debugrawgame?"
;
; Hero commands/actions:
; - Move(dir): N, E, S, W (All can cause attacks on an adjacent monster)
; - Drop(item): Drop item from sack (only if full and floor is clear)
; - Read(scroll): Read scroll currently in the sack
; - Quaff(potion): Quaff potion currently in the sack
; - Throw(arrow,dir): Throws arrow in quiver from sack (direction dir)
;
; Properties of objects:
; - All objects (armor, amulet, potions, scrolls, weapons)
;   - Are picked up automatically when moved on, if room in sack
;   - burden=1 (except gold)
;   - Can be dropped (if sack is full)
; - Sack
;   - Has a max capacity (maxBurden)
;   - Items wielded or worn continue to count towards carrying burden
; - Armor: Defense values in [1,3]
;   - None: 1         ; No defense against monsterattacks
;   - Ring mail: 2    ; First monster attack does not decrease health
;   - Banded mail: 3  ; First 2 monster attacks do not decrease health
; - Potions (deleted after use)
;   - Speed (Doubles speed for 5 turns)
;   - Strength (Increases attack value by 1 for 5 turns)
;   - Invisibility (note: cannot implement blindness)
; - Scrolls (deleted after use)
;   - Restore health (to maximum value)
;   - Armor enhance (Increases armor strength by 1, to max 3)
;   - Sleep (forces sleeping for 2 turns)
; - Weapon: Offensive value in [1,3]
;   - None: 1 (melee only)
;   - Mace: 2 (melee only)
;   - Sword: 3 (melee only)
;   - Bow: 1 (when throwing arrow) (bow+quiver has burden of 1)
;     - Can only throw an arrow when wielding a bow
;       - Decrements number of arrows in a quiver
;       - Assume arrow always hit a target if it is thrown towards target
;       - Assume arrow has unlimited range, and disappears after thrown 
; - Monsters: Name (Character) [Initial health, Damage when attacking, Base speed]
;   - Bat (B) [3, 2, 1]: Asleep unless/until attacked
;   - Hobgoblin (H) [5, 3, 1]: Toughest monster in domain
;   - Kestral (K) [2, 2, 2]: Moves at twice speed unless hitting
;   - Snake (S) [4, 2, 0.5]: Moves at half speed
;
; Notes:
;   0. Turn-taking: The hero and the monsters rotate turns
;   1. The hero cannot share a location with a monster.
;   2. When in the same room or corridor as a monster that is not asleep,
;      the monster will advance towards the hero.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GDL State Machine Model Components:
;   1. Role: Players of the game
;   2. Init: Facts true in the initial state (initial state of fluents)
;   3. Transitions: Rules for determining what is/will be true in the world
;   4. Legal: Legal moves for a <player,state> pair
;   5. Goal: Value of a (final) state for a player
;   6. Terminal: Terminal state
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 1. Role
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Meaning: This is a one-player game
(role hero)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 2. Init
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;---------------------------------------------------------------------------
; 2.1 Things that differ among tasks
;---------------------------------------------------------------------------
; >>>>>
; Monsters and their types
(monster hobgoblin1 hobgoblin)
(monster bat1 bat)

; Initial health of Hero and monsters (hero's health is defined in 2.3)
(init (health hobgoblin1 5)) 
(init (health bat1 3))

; Items (that can be picked up by Hero) (amulet & gold are defined in 2.3)
(item armor1)
(item scroll1)
(item scroll2)
(item scroll3)

; Item typings (note: a bow includes a quiver and a set of arrows)
(armor armor1 bandedmail)
(scroll scroll1 armorEnhance)
(scroll scroll2 sleep)
(scroll scroll3 restoreHealth)

; Locations of character and items (North Room)
(init (location armor1 1 1))
(init (location gold 5 1))
(init (location hero 0 2))         ; our Hero
(init (location bat1 3 2))         ; Monster
(init (location scroll3 4 1))

; Locations of character and items (South Room)
(init (location hobgoblin1 2 4))     ; Monster
(init (location amulet 3 4))
(init (location scroll1 3 5))
(init (location scroll2 5 4))
(init (location exit 5 5))           ; exit
; <<<<<

;---------------------------------------------------------------------------
; 2.2 Rooms and corridor
; - Same representation of walls as Wargame and Escape
;---------------------------------------------------------------------------
; Room: <topLeft_x topLeft_y bottomRight_x bottomRight_y>
(room 0 0 5 2)    ; North room
(room 0 4 5 5)    ; South room

; Corridor location: <x y>
(corridor 4 3) ; Corridor/doorway between north and south rooms
(init (blocked west 4 3))
(init (blocked east 4 3))

; North room
(init (blocked west 0 0))
(init (blocked west 0 1))
(init (blocked west 0 2))
(init (blocked north 0 0))
(init (blocked north 1 0))
(init (blocked north 2 0))
(init (blocked north 3 0))
(init (blocked north 4 0))
(init (blocked north 5 0))
(init (blocked east 5 0))
(init (blocked east 5 1))
(init (blocked east 5 2))
(init (blocked south 0 2))
(init (blocked south 1 2))
(init (blocked south 2 2))
(init (blocked south 3 2))
(init (blocked south 5 2))

; South room
(init (blocked west 0 4))
(init (blocked west 0 5))
(init (blocked north 0 4))
(init (blocked north 1 4))
(init (blocked north 2 4))
(init (blocked north 3 4))
(init (blocked north 5 4))
(init (blocked east 5 4))
(init (blocked east 5 5))
(init (blocked south 0 5))
(init (blocked south 1 5))
(init (blocked south 2 5))
(init (blocked south 3 5))
(init (blocked south 4 5))
(init (blocked south 5 5))

;---------------------------------------------------------------------------
; 2.3 Things we don't typically change from one task to another
;---------------------------------------------------------------------------
; Player starts at step/state 1. Game has finite length.
(init (step 1))

; Items (that can be picked up by the Hero) 
(item amulet)
(item gold)

; Hero's initial health
(init (health hero 4))

; Burden/1: <burdenamount>
; Meaning: Initially the sack is empty.
(init (burden 0))

; Quiversize/2: <size>
(init (quiverSize 5))

; Parity counters for half & quarter speed movement
(init odd)
(init (slomo-time 1))

; The exit location
(exitType exit)

;----------------------------------------------------------------------
; 2.4 Constants
;----------------------------------------------------------------------

; Meaning: Sack can contain at most this number of items
(maxBurden 5)

; Meaning: Maximum health a hero can ever attain
(maxHealth hero 4)

; Meaning: The maximum defined time-step (t = 30)
(maxTime 30)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 3. Transitions: Rules used to reason about states
;    - Specifies the dynamics of fluents
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;----------------------------------------------------------------------
; 3.1 Location updates
;----------------------------------------------------------------------

; Meaning: Blocked locations are persistent
(<= (next (blocked ?dir ?x ?y))
   (true (blocked ?dir ?x ?y)))

; Meaning: Updates hero location
(<= (next (location hero ?x ?y))
   (nextHeroLocation ?x ?y))                 ; Action

; Meaning: Updates monster location
(<= (next (location ?monster ?x ?y))
   (monster ?monster ?monster-type)
   (true (health ?monster ?health))
   (> ?health 0)                             ; built-in
   (nextMonsterLocation ?monster ?x ?y))     ; Action

; Meaning: The exit does not move
(<= (next (location exit ?x ?y))
   (true (location exit ?x ?y)))

; Meaning: Items remain on the ground until they are picked up
(<= (next (location ?item ?x ?y))
   (item ?item)
   (true (location ?item ?x ?y))
   (not (pickedUp ?item)))        ; Object-interaction

; Meaning: Dropped objects are placed at the hero's current location
(<= (next (location ?item ?x ?y))
   (does hero (drop ?item))
   (true (location hero ?x ?y)))

; DA (7/13): Must also drop a weapon if picking up another
(<= (next (location ?oldWeapon ?x ?y))
   (changeWeapon ?oldWeapon)            ; DA: We pickedUp another!
   (true (location hero ?x ?y)))

;----------------------------------------------------------------------
; 3.2 Health updates for hero
;     - If the hero is attacked, his health is decreased by difference
;       of 1 + (monster's attack value - hero defense value)
;     - Health goes to max if hero reads restoreHealth scroll
;----------------------------------------------------------------------

; Meaning: Hero incurs damage
; Note: totalMonsterDamage maxes out at hero's current health, so
; we shouldn't need to check for negative health here.
; DA: If the restore health scroll is simultaneously read, and
; monster damage is nonzero, then the scroll has no effect!
(<= (next (health hero ?hpNew))
   (totalMonsterDamage ?hitPointsLost)
   (> ?hitPointsLost 0)
   (true (health hero ?hpOld))
   (- ?hpOld ?hitPointsLost ?hpNew))

; Hero recovers a hit point (HP) iff not attacked/etc.
(<= (next (health hero ?hpNew))
    (totalMonsterDamage 0)
    (not (healthRestoredByScroll))
    (true (health hero ?hpOld))
    (maxHealth hero ?maxHealth)
    (> ?maxHealth ?hpOld)
    (+ ?hpOld 1 ?hpNew))

; Meaning: Hero already at maxHealth and not attacked
(<= (next (health hero ?maxHealth))
   (totalMonsterDamage 0)
   (maxHealth hero ?maxHealth)
   (true (health hero ?maxHealth)))

; Meaning: Hero read a restoreHealth scroll to regain maximum health
(<= (next (health hero ?maxHealth))
   (totalMonsterDamage 0)
   (healthRestoredByScroll)
   (maxHealth hero ?maxHealth))

;----------------------------------------------------------------------
; 3.3 Health updates for monsters
;     - If attacked by hero, health decreases by attack value of hero's
;       wielded weapon!
;     - Simplification: Only arrows can be thrown, and only when hero
;       wields a bow
;----------------------------------------------------------------------

; Meaning: Hero kills the monster (melee)
(<= (next (health ?monster 0))
   (monster ?monster ?monster-type)
   (monsterAttacked ?monster)
   (true (health ?monster ?hpOld))
   (heroOffense ?wDamage)
   (>= ?wDamage ?hpOld))

; Meaning: Hero hurts the monster (melee)
(<= (next (health ?monster ?hpNew))
   (monster ?monster ?monster-type)
   (monsterAttacked ?monster)
   (true (health ?monster ?hpOld))
   (heroOffense ?wDamage)
   (> ?hpOld ?wDamage)
   (- ?hpOld ?wDamage ?hpNew))

; Meaning: Hero kills the monster (by throwing an arrow)
; Hardcoding the damage value of arrows: 2
(<= (next (health ?monster 0))
   (monster ?monster ?monster-type)
   (hitByArrow ?monster)
   (true (health ?monster ?hpOld))
   (>= 2 ?hpOld))

; Meaning: Hero hurts the monster (by throwing an arrow)
; Hardcoding the damage value of arrows: 2
(<= (next (health ?monster ?hpNew))
   (monster ?monster ?monster-type)
   (true (health ?monster ?hpOld))
   (> ?hpOld 2)
   (hitByArrow ?monster)
   (- ?hpOld 2 ?hpNew))

; Meaning: Unattacked monster's health is unchanged
(<= (next (health ?monster ?hp))
   (monster ?monster ?monster-type)
   (not (monsterAttacked ?monster))
   (not (hitByArrow ?monster))
   (true (health ?monster ?hp)))

;----------------------------------------------------------------------
; 3.4 Burden-related updates
;     - Sack, armor, and weaponry
;----------------------------------------------------------------------

;--------------------------------------------------
; 3.4.1: Burden maintenance relations
;--------------------------------------------------

; Meaning: Items remain in hero's sack when picked up
(<= (next (carrying ?item))
   (item ?item)
   (pickedUp ?item))                  ; Object-interaction

; Meaning: Items in sack stay in sack unless dropped
(<= (next (carrying ?item))
   (true (carrying ?item))
   (not (changeWeapon ?item))         ; It's not a weapon we're auto-dropping
   (not (decrementsBurden ?item)))

; Meaning: Maintain size of burden if it's not changed
(<= (next (burden ?burden))
   (not burdenChanged)
   (true (burden ?burden)))

; Meaning: Increment burden by picking up an item
; Note: we don't have to check maxBurden here, because it's checked in pickedUp.
(<= (next (burden ?newBurden))
   (pickedUp ?item)                   ; Object-interaction
   (distinct ?item gold)
   (not (weaponChange ?item))         ; DA (7/13): burden doesn't change when changing weapons
   (true (burden ?oldBurden))
   (succ ?oldBurden ?newBurden))      ; Arithmetic

; Meaning: Decrement burden by dropping an item or using a scroll or potion
(<= (next (burden ?newBurden))
   (decrementsBurden ?item)
   (true (burden ?oldBurden))
   (succ ?newBurden ?oldBurden))      ; Arithmetic

;--------------------------------------------------
; 3.4.2 Quiver size (i.e., # arrows in it)
;--------------------------------------------------

; Meaning: Quiver size doesn't change when no arrows are thrown.
; Note: we don't have to check here for wielding the bow or
; carrying it.  If that's not true, then hero can't legally
; throw an arrow.
; Also: don't reify the quiver apart from the bow.  It's a
; package, like a clip of ammo in a gun.
(<= (next (quiverSize ?oldSize))
   (not arrowThrown)
   (true (quiverSize ?oldSize)))

; Meaning: Quiver size decreases when throwing arrow w/ a bow
; - Ensures quiver contains at least one arrow
(<= (next (quiverSize ?newSize))
   (does hero (throw arrow ?dir))
   (true (quiverSize ?oldSize))
   (- ?oldSize 1 ?newSize))           ; Arithmetic

;--------------------------------------------------
; 3.4.3: Armor-wearing relations
; -- DA (7/13): Only 1 armor max in game!!!
;--------------------------------------------------

; Meaning: Hero continues wearing armor until it is dropped
(<= (next (wearing ?item))
   (true (wearing ?item))
   (not (does hero (drop ?item))))  ; note: drops can occur only if sack is full

; Meaning: Hero is wearing armor that is newly picked up
(<= (next (wearing ?armor))
   (armor ?armor ?armor-type)
   (pickedUp ?armor))

;--------------------------------------------------
; 3.4.4: Weapon-wielding relations
;--------------------------------------------------

; Meaning: Continue wielding a weapon wielded unless exchanging or dropping
(<= (next (wielding ?weapon))
   (true (wielding ?weapon))
   (not (changeWeapon ?weapon))      
   (not (does hero (drop ?weapon)))) ; note: drops can occur only if sack is full

; Meaning: Wield a weapon automatically if it is picked up
; Note: Any currently-wielded weapon would be dropped (elsewhere)
(<= (next (wielding ?weapon))
   (weapon ?weapon ?weapon-type)
   (pickedUp ?weapon))

;--------------------------------------------------
; 3.4.5: Sleep relations
;--------------------------------------------------

; Meaning: Reading the sleep scroll induces 2 turns of sleep
(<= (next (asleep hero 2))
   (does hero (read ?scroll))
   (scroll ?scroll sleepInduce))

; Meaning: Sleep decrement
(<= (next (asleep hero ?n))
   (true (asleep hero ?old-n))
   (distinct ?old-n 1)
   (- ?old-n 1 ?n))

; Meaning: Let sleeping monsters sleep
(<= (next (asleep ?monster ?n))
   (monster ?monster ?monster-type)
   (true (asleep ?monster ?n))
   (true (health ?monster ?hp))
   (distinct ?hp 0)
   (not (provoked ?monster))) ; in this case, attacked or hit with an arrow

;--------------------------------------------------
; 3.4.6: Visibility
;--------------------------------------------------

;;; Meaning: hero is invisible to monsters for the next 5 turns
(<= (next (invisible hero 5))
   (does hero (quaff ?potion))
   (potion ?potion invisibility))

(<= (next (invisible hero ?n))
   (true (invisible hero ?old-n))
   (distinct ?old-n 1)
   (- ?old-n 1 ?n))

;--------------------------------------------------
; 3.4.7: Enhanced Speed, Strength, Armor
;--------------------------------------------------

; Meaning: Hero's speed is enhanced by 1 for 5 turns
(<= (next (enhancedSpeed hero 5))
   (does hero (quaff ?potion))
   (potion ?potion speed))

(<= (next (enhancedSpeed hero ?n))
   (true (enhancedSpeed hero ?old-n))
   (distinct ?old-n 1)
   (- ?old-n 1 ?n))

; Meaning: Hero's attack strength is enhanced by 1 for 5 turns
(<= (next (enhancedStrength hero 5))
   (does hero (quaff ?potion))
   (potion ?potion strength))

(<= (next (enhancedStrength hero ?n))
   (true (enhancedStrength hero ?old-n))
   (distinct ?old-n 1)
   (- ?old-n 1 ?n))

(<= (next (enhancedArmor hero 5))
   (does hero (read ?scroll))
   (scroll ?scroll armorEnhance)
   (true (wearing ?armor))  ; You have to be wearing armor first
   (armor ?armor ?armor-type)
   (armorstrength ?armor-type ?amt)
   (>= 3 ?amt))

(<= (next (enhancedArmor hero ?n))
    (true (enhancedArmor hero ?old-n))
    (distinct ?old-n 1)
    (- ?old-n 1 ?n))

;---------------------------------------------------------------------------
;;; 3.5 Standard GDL support rule
;---------------------------------------------------------------------------

; Meaning: Time advances in one step increments
(<= (next (step ?next))
   (true (step ?current))
   (succ ?current ?next))          ; Arithmetic

;---------------------------------------------------------------------------
;;; 3.6 Movement speed rules
; SW: Changed to only update the parity counters when they are being used.
; This isn't a useless groundings problem, but it actually breaks our
; spatial assumptions, since if we see a game state change, the agent
; can use no knowledge about what directions are redundant to move
; in. So we need to minimize useless game state changes.
;---------------------------------------------------------------------------
(<= (monsterIsMoving)
   (provoked ?monster))

(<= (slomoMonsterMoving ?monster)
   (currentlyEnhancedSpeed hero)
   (provoked ?monster)
   (monster ?monster ?monster-type)
   (monsterSpeed ?monster-type .5))

; Parity counter for 1/2 speed movement
(<= (next odd)
   (monsterIsMoving)
   (true even))
(<= (next even)
   (monsterIsMoving)
   (true odd))
(<= (next even)
   (not (monsterIsMoving))
   (true even))
(<= (next odd)
   (not (monsterIsMoving))
   (true odd))

; Meaning: slow-motion or "matrix" time counter for sluggish snakes
(<= (next (slomo-time 1))
   (slomoMonsterMoving ?monster)
   (true (slomo-time 4)))
(<= (next (slomo-time 2))
   (slomoMonsterMoving ?monster)
   (true (slomo-time 1)))
(<= (next (slomo-time 3))
   (slomoMonsterMoving ?monster)
   (true (slomo-time 2)))
(<= (next (slomo-time 4))
   (slomoMonsterMoving ?monster)
   (true (slomo-time 3)))

(<= (next (slomo-time ?t))
   (true (slomo-time ?t))
   (not (currentlyEnhancedSpeed hero)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 4. Legal: Specifies legal moves for players
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Meaning: The hero can rest (and do nothing) at any time
; (legal hero (rest))

; Meaning: The hero may move anywhere within rooms and corridors
; - BUT: Moving to a monster's location is an attack, not a move!
(<= (legal hero (move ?dir))
   (not (currentlyAsleep hero))
   (true (location hero ?xOld ?yOld))
   (direction ?dir)                        ; Generic spatial
   (nextCell ?dir ?xOld ?yOld ?xNew ?yNew) ; Movement
   (traversible ?xNew ?yNew))              ; Generic spatial

; Meaning: Hero can drop an item (being carried) if sack is full
(<= (legal hero (drop ?item))
   (true (burden ?max-burden))             ; New constraint (7/13)
   (maxBurden ?max-burden)                 ; New constraint (7/13)
   (not (currentlyAsleep hero))
   (true (carrying ?item)))

; Meaning: Hero can read a scroll (being carried)
(<= (legal hero (read ?scroll))
   (not (currentlyAsleep hero))
   (true (carrying ?scroll))
   (scroll ?scroll ?scrollType))            ; Init

; Meaning: Hero can quaff a potion (being carried)
(<= (legal hero (quaff ?potion))
   (not (currentlyAsleep hero))
   (true (carrying ?potion))
   (potion ?potion ?potionType))            ; Init

; Meaning: Hero can throw an arrow (if carrying a non-empty quiver)
(<= (legal hero (throw arrow ?dir))
   (not (currentlyAsleep hero))
   (true (wielding ?weapon))
   (weapon ?weapon bow)                    ; Init
   (true (quiverSize ?numarrows))
   (> ?numarrows 0)
   (direction ?dir))                       ; Generic spatial

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 5. Goal: Value of a (final) state for a player
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Truth table for goals in mRogue TL Level 6
;;; (Thanks to Tom Hinrichs & Matt Molineaux, 9/6/07)
;              100%     50%   50%    30%    0%
; ---------------------------------------------------
; Health != 0     t        t     t     t    nil
; Amulet          t        t     nil   nil
; atExit          t        nil   t     nil

; Meaning: 100 points if hero survives, w/ amulet, and exits!
(<= (goal hero 100)
    (not (true (health hero 0)))    ; Endgame
    (true (carrying amulet))        ; The Hero must carry the amulet
    atExit)                         ; Endgame

; Meaning: 50 points if hero survives, w/ amulet, but hasn't exited
(<= (goal hero 50)
    (not (true (health hero 0)))    ; Endgame
    (true (carrying amulet))
    (not atExit))                   ; Endgame

; Meaning: 50 points if hero survives, w/o amulet, but exits
(<= (goal hero 50)
    (not (true (health hero 0)))    ; Endgame
    (not (true (carrying amulet)))
    atExit)                         ; Endgame

; Meaning: 30 points if hero survives w/o amulet or exiting
(<= (goal hero 30)
    (not (true (health hero 0)))    ; Endgame
    (not (true (carrying amulet)))
    (not atExit))                   ; Endgame

; Meaning: 0 points if hero is killed
(<= (goal hero 0)
    (true (health hero 0)))         ; Endgame

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 6. Terminal: Terminal state
;    - specifies the conditions under which the game has ended
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Meaning: The game is over if the hero is killed
(<= terminal
   (true (health hero 0)))         ; Endgame

; Meaning: The game is over if time runs out
(<= terminal
   (true (step ?t))
   (maxTime ?t))                   ; Endgame

; Meaning: The game is over if the hero has escaped with the amulet
(<= terminal
   (true (carrying amulet))
   atExit)                         ; Endgame

;=============================================================================
; Supporting (Low-Level) Relations
;=============================================================================

;----------------------------------------------------------------------
; Battle relations
;----------------------------------------------------------------------

; Meaning: The amount of damage incurred by monster attacks (on possibly
; two fronts).  This should return 0 if hero is not attacked and should
; max out at hero's current health.
(<= (totalMonsterDamage 0)
   (nextHeroLocation ?x ?y)
   (not (attackedBySomeMonster ?x ?y)))

; Meaning: First monster damage
(<= (totalMonsterDamage ?damage)
   (nextHeroLocation ?x ?y)
   (monster ?monster ?monster-type)
   (attackedByMonster ?monster ?x ?y)
   (monsterStrength ?monster-type ?strength)
   (totalMonsterDamage1 ?monster ?strength ?x ?y ?damage))

; Meaning: No second monster attack
(<= (totalMonsterDamage1 ?monster ?strength ?x ?y ?damage)
   (nextHeroLocation ?x ?y) ; SW added
   (monster ?monster ?monster-type) ; SW added
   (attackedByMonster ?monster ?x ?y) ; SW added   ****
   (not (attackedBy2ndMonster ?monster ?x ?y))
   (amountOfMonsterDamage ?strength ?d1)
   (true (health hero ?hp))
   (min ?d1 ?hp ?damage))

; Meaning: Check second monster damage
(<= (totalMonsterDamage1 ?monster1 ?strength ?x ?y ?damage)
   (totalMonsterDamage2 ?monster1 ?strength ?x ?y ?damage))

; Meaning: combined damage from two monsters
(<= (totalMonsterDamage2 ?monster1 ?strength1 ?x ?y ?damage)
   (nextHeroLocation ?x ?y) ; SW added
   (monster ?monster2 ?monster-type)
   (monster ?monster1 ?monster-type) ; SW added   ****
   (distinct ?monster2 ?monster1)
   (attackedByMonster ?monster1 ?x ?y) ; SW added   ****
   (attackedByMonster ?monster2 ?x ?y)
   (monsterStrength ?monster-type ?strength1) ; SW added  ****
   (monsterStrength ?monster-type ?strength2)
   (+ ?strength1 ?strength2 ?strength)
   (amountOfMonsterDamage ?strength ?combined-damage)
   (true (health hero ?hp))
   (min ?combined-damage ?hp ?damage))

; Meaning: There exists some monster that attacks hero at ?x ?y
(<= (attackedBySomeMonster ?x ?y)
   (monster ?monster ?monster-type)
   (attackedByMonster ?monster ?x ?y))

; Meaning: Monster attacks hero when provoked and within one spot of
; hero's next location
(<= (attackedByMonster ?monster ?x2 ?y2)
   (provoked ?monster)                            ; Generic monster
   (nextHeroLocation ?x2 ?y2) ; SW added  ****
   (true (location ?monster ?x1 ?y1))
   (adjacent ?x1 ?y1 ?x2 ?y2))                    ; Generic spatial

; Meaning: Monster1 and monster2 both attack simultaneously.
(<= (attackedBy2ndMonster ?monster1 ?x ?y)
   (monster ?monster2 ?monster-type)
   (monster ?monster1 ?monster-type) ; SW   ****
   (distinct ?monster2 ?monster1)
   (attackedByMonster ?monster2 ?x ?y)
   (attackedByMonster ?monster1 ?x ?y))

; Meaning: No damage if monster's attack is less than defense
; Offensive-strength is the combined strength of attacking monsters.
; - Open to debate
(<= (amountOfMonsterDamage ?offensive-strength 0)
   (attackedByMonster ?monster ?x ?y)                  ; SW   ****
   (monster ?monster ?monster-type)                    ; SW   ****
   (monsterStrength ?monster-type ?offensive-strength) ; SW   ****
   (heroDefense ?defense)                          ; Battle
   (> ?defense ?offensive-strength)) ; SW ****: changed from >=, bug? Would imply that no damage occurs if offense == defense..

; Meaning: Attacks whose value is equal to or greater than armor value
; - Open to debate (i.e., Attack value is 1 + (offense - defense))
(<= (amountOfMonsterDamage ?offensive-strength ?hitPointsLost)
   (attackedByMonster ?monster ?x ?y)                  ; SW   ****
   (monster ?monster ?monster-type)                    ; SW   ****
   (monsterStrength ?monster-type ?offensive-strength) ; SW   ****
   (heroDefense ?defense)                          ; Battle
   (>= ?offensive-strength ?defense) ; SW: this seems necessary
   (-  ?offensive-strength ?defense ?diff)         ; Arithmetic
   (+ ?diff 1 ?hitPointsLost))                     ; Arithmetic

; Meaning: The current defensive strength of armor worn (if any)
(<= (heroDefense 1)
   (not (armored hero)))

(<= (heroDefense ?defense)
   (not (currentlyEnhancedArmor hero))
   (true (wearing ?item))
   (armor ?item ?armor-type)
   (armorstrength ?armor-type ?defense))

(<= (heroDefense ?defense)
   (int ?n)         ; ?n is how many turns it will remain in effect (0 - 5)
   (true (enhancedArmor hero ?n))
   (true (wearing ?item))
   (armor ?item ?armor-type)
   (armorstrength ?armor-type ?base-defense)
   (+ ?base-defense 1 ?defense))

; Meaning: The current offense strength of weapons wielded (if any)
(<= (heroOffense 1)
   (not (armed hero)))

(<= (heroOffense ?offense)
   (not (currentlyEnhancedStrength hero))
   (true (wielding ?weapon))
   (weapon ?weapon ?weapon-type)                  ; Init
   (weaponStrength ?weapon-type ?offense))        ; Generic weapon

(<= (heroOffense ?offense)
   (currentlyEnhancedStrength hero)
   (true (wielding ?weapon))
   (weapon ?weapon ?weapon-type)                  ; Init
   (weaponStrength ?weapon-type ?offense))        ; Generic weapon SW: fixed variable name

; Meaning: Hero attacks the monster
(<= (monsterAttacked ?monster)
   (true (location hero ?x1 ?y1))
   (true (location ?monster ?x2 ?y2))
   (monster ?monster ?monster-type)
   (adjacent ?x1 ?y1 ?x2 ?y2)
   (does hero (move ?dir))
   (nextCell ?dir ?x1 ?y1 ?x2 ?y2))

;----------------------------------------------------------------------
; Movement relations (non-recursive)
;----------------------------------------------------------------------

; Meaning: Exhausted our travel distance
(<= (nStepsTowardsHero 0 ?startx ?starty ?startx ?starty)
    (cellNearMonster ?startx ?starty)) ; SW

(<= (nStepsTowardsHero 1 ?startx ?starty ?finalx ?finaly)
    (cellNearMonster ?startx ?starty) ; SW
    (cellNearMonster ?finalx ?finaly) ; SW
    (singleStepTowardsHero ?startx ?starty ?finalx ?finaly))

(<= (nStepsTowardsHero 2 ?startx ?starty ?finalx ?finaly)
  (cellNearMonster ?startx ?starty)
  (cellNearMonster ?finalx ?finaly)
  (singleStepTowardsHero ?startx ?starty ?x2 ?y2)
  (cellNearMonster ?x2 ?y2)
  (singleStepTowardsHero ?x2 ?y2 ?finalx ?finaly))

; Meaning: Take one step toward destination only if you won't run into hero.
(<= (singleStepTowardsHero ?startx ?starty ?nextx ?nexty)
    (oneStepTowardsHero ?startx ?starty ?nextx ?nexty)
    (traversible ?nextx ?nexty)
    (nextHeroLocation ?herox ?heroy)
    (not (sameLocation ?nextx ?nexty ?herox ?heroy)))

; Meaning: Stay put and attack the hero.
(<= (singleStepTowardsHero ?startx ?starty ?startx ?starty)
    (nextHeroLocation ?herox ?heroy)
    (oneStepTowardsHero ?startx ?starty ?herox ?heroy))

;;; Rather than having horizontal or vertical movement preferences
;;; as in Wargame, in mRogue we alternate each turn.

; Meaning: Move one step (horizontally or vertically) towards destination
(<= (oneStepTowardsHero ?x1 ?y1 ?x3 ?y1)  ; go west
   (nextHeroLocation ?x2 ?y1)
   (cellNearMonster ?x1 ?y1)
   (> ?x1 ?x2)
   (- ?x1 1 ?x3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x3 ?y1)  ; go east
   (nextHeroLocation ?x2 ?y1)
   (cellNearMonster ?x1 ?y1)
   (> ?x2 ?x1)
   (+ ?x1 1 ?x3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x1 ?y3)  ; go north
   (nextHeroLocation ?x1 ?y2)
   (cellNearMonster ?x1 ?y1)
   (> ?y1 ?y2)
   (- ?y1 1 ?y3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x1 ?y3)  ; go south
   (nextHeroLocation ?x1 ?y2)
   (cellNearMonster ?x1 ?y1)
   (> ?y2 ?y1)
   (+ ?y1 1 ?y3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x3 ?y1)    ; go west
   (nextHeroLocation ?x2 ?y2)
   (cellNearMonster ?x1 ?y1)
   (> ?x1 ?x2)
   (distinct ?y1 ?y2)
   (true odd)
   (- ?x1 1 ?x3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x3 ?y1)    ; go east
   (nextHeroLocation ?x2 ?y2)
   (cellNearMonster ?x1 ?y1)
   (> ?x2 ?x1)
   (distinct ?y1 ?y2)
   (true odd)
   (+ ?x1 1 ?x3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x1 ?y3)  ; go north
   (nextHeroLocation ?x2 ?y2)
   (cellNearMonster ?x1 ?y1)
   (> ?y1 ?y2)
   (distinct ?x1 ?x2)
   (true even)
   (- ?y1 1 ?y3))

(<= (oneStepTowardsHero ?x1 ?y1 ?x1 ?y3)  ; go south
   (nextHeroLocation ?x2 ?y2)
   (cellNearMonster ?x1 ?y1)
   (> ?y2 ?y1)
   (distinct ?x1 ?x2)
   (true even)
   (+ ?y1 1 ?y3))

; Meaning: Direction-based relationships between coordinates
; - Read: one cell in ?direction of ?x1 ?y1 is ?x2 ?y2
; I had hoped to get rid of this, but it's used in determining
; legal moves.
(<= (nextCell north ?x ?y1 ?x ?y2)
   (int ?x)
   (int ?y1) ; SW- new groundings
   (- ?y1 1 ?y2))
(<= (nextCell south ?x ?y1 ?x ?y2)
   (int ?x)
   (int ?y1) ; SW- new groundings
   (+ ?y1 1 ?y2))
(<= (nextCell east ?x1 ?y ?x2 ?y)
   (int ?y)
   (int ?x1) ; SW- new groundings
   (+ ?x1 1 ?x2))
(<= (nextCell west ?x1 ?y ?x2 ?y)
   (int ?y)
   (int ?x1) ; SW- new groundings
   (- ?x1 1 ?x2))

;----------------------------------------------------------------------
; Generic spatial relations
;----------------------------------------------------------------------

; Roomlocation: <x y>  (derived from "room")
(<= (roomlocation ?x ?y)
   (room ?tlx ?tly ?brx ?bry)
   (between ?tlx ?x ?brx)        ; Generic spatial
   (between ?tly ?y ?bry))       ; Generic spatial

; Meaning: ?a <= ?b <= ?c (note: permits them all to be equal)
(<= (between ?a ?b ?c)
   (int ?a) ; SW- must ground these
   (int ?b)
   (int ?c)
   (>= ?b ?a)                    ; Arithmetic
   (>= ?c ?b))                   ; Arithmetic

; Meaning: <x,y> can be traversed by a character (or hold an item)
; We actually don't need this for monster movement because so long
; as they only pursue hero within a room, they'll never try to
; go through walls or cut corners.  If we change monster behavior
; or have non-rectangular rooms, then oneStepTowards will have to
; check.
(<= (traversible ?x ?y)
    (roomlocation ?x ?y))   ; Generic spatial
(<= (traversible ?x ?y)
    (corridor ?x ?y))     ; Generic spatial

; Meaning: Four possible directions of movement
(direction north)
(direction south)
(direction east)
(direction west)

(verticalDir north)
(verticalDir south)
(horizontalDir east)
(horizontalDir west)

; Meaning: Same location
(<= (sameLocation ?x ?y ?x ?y) ; SW -- must be ints
    (int ?x)
    (int ?y))

; Meaning: Adjacent locations (diagonal is not adjacent)
(<= (adjacent ?x ?y1 ?x ?y2)
    (int ?x)      ; SW
    (diff ?y1 ?y2 1))
(<= (adjacent ?x1 ?y ?x2 ?y)
    (int ?y)      ; SW
    (diff ?x1 ?x2 1))

;----------------------------------------------------------------------
; Generic monster relations
;----------------------------------------------------------------------
(initialHealth   bat 3)
(monsterStrength bat 2)
(monsterSpeed    bat 1)

(initialHealth hobgoblin 5)
(monsterStrength hobgoblin 3)
(monsterSpeed hobgoblin 1)

(initialHealth kestral 2)
(monsterStrength kestral 2)
(monsterSpeed kestral 2)

(initialHealth snake 4)
(monsterStrength snake 2)
(monsterSpeed snake 0.5)

(speedNumber 0.5) ; SW: need these so we can ground potential speeds.
(speedNumber 1)
(speedNumber 2)

; Meaning: Monsters attack when in the same room as the hero.
; If they're asleep, they wake up when hero attacks them or
; throws an arrow at them.
(<= (provoked ?monster)
   (monster ?monster ?monster-type)
   (not (currentlyAsleep ?monster))
   (true (location ?monster ?x ?y))
   (room ?lx ?ty ?rx ?by)
   (between ?lx  ?x ?rx)
   (between ?ty ?y ?by)
   (nextHeroLocation ?hx ?hy)
   (between ?lx  ?hx ?rx)
   (between ?ty ?hy ?by))

(<= (provoked ?monster)
   (monster ?monster ?monster-type)
   (true (asleep ?monster ?n))
   (monsterAttacked ?monster))

(<= (provoked ?monster)
   (monster ?monster ?monster-type)
   (true (asleep ?monster ?n))
   (hitByArrow ?monster))

;----------------------------------------------------------------------
; Generic item relations
;----------------------------------------------------------------------

; Armor strength (defense)
(armorstrength ringmail 2)
(armorstrength bandedmail 3)

; Weapon strength (offense)
(weaponStrength mace 2)
(weaponStrength sword 3)
(weaponStrength bow 1)


;----------------------------------------------------------------------
; Endgame relations
;----------------------------------------------------------------------

; Mearning: True if the hero is at the same location as the exit
(<= atExit
   (true (location hero ?x ?y))
   (true (location exit ?x ?y)))

;----------------------------------------------------------------------
; Arithmetic relations
;----------------------------------------------------------------------

; Meaning: Absolute value of difference
(<= (diff ?n1 ?n2 ?diff)
   (int ?n1) ; SW- new groundings
   (int ?n2) ; SW- new groundings
   (> ?n1 ?n2)
   (- ?n1 ?n2 ?diff))

(<= (diff ?n1 ?n2 ?diff)
   (int ?n1) ; SW- new groundings
   (int ?n2) ; SW- new groundings
   (>= ?n2 ?n1)
   (- ?n2 ?n1 ?diff))


; Meaning: Minimum
(<= (min ?n1 ?n2 ?n2)
   (int ?n1) ; SW: must ground these
   (int ?n2)
   (> ?n1 ?n2))
(<= (min ?n1 ?n2 ?n1)
   (int ?n1) ; SW: must ground these
   (int ?n2)
   (>= ?n2 ?n1))


; Meaning: Defines a (finite) succesor relation
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)

; Meaning: The integers in [0,7]
; SW -- these are used in lots of other places now
(int 0)
(int 1)
(int 2)
(int 3)
(int 4)
(int 5)
(int 6)
(int 7)

;---------------------------------------------------------------------------
; Action relations
;---------------------------------------------------------------------------

; Meaning: Hero moves one cell in direction ?dir from ?x1 ?y1
; - If <x1,y1> is not currently occupied by a monster.
(<= (nextHeroLocation ?x2 ?y2)
   (true (location hero ?x1 ?y1))
   (does hero (move ?dir))
   (nextCell ?dir ?x1 ?y1 ?x2 ?y2)
   (not (monsterAt ?x2 ?y2)))

; Meaning: Hero's location is unchanged if attacking a monster
(<= (nextHeroLocation ?x1 ?y1)
   (true (location hero ?x1 ?y1))
   (does hero (move ?dir))
   (nextCell ?dir ?x1 ?y1 ?x2 ?y2)
   (monsterAt ?x2 ?y2))

; Meaning: Non-moving hero actions
(<= (nextHeroLocation ?x ?y)
   (true (location hero ?x ?y))
   (not (heroMoves)))

; Meaning: Hero moved in some direction
(<= (heroMoves)
   (does hero (move ?dir)))

; Meaning: There's a monster currently at ?x ?y
(<= (monsterAt ?x ?y)
   (true (location ?monster ?x ?y))
   (monster ?monster ?monster-type))

; Meaning: Move provoked monsters towards hero according to ?speed
; - BUT: Cannot stop on same location as hero's location
;   We'll make nStepsTowardsHero stop when the next location will be
;   occupied by the hero.
(<= (nextMonsterLocation ?monster ?x2 ?y2)
   (not (currentlyInvisible hero))
   (provoked ?monster)
   (true (location ?monster ?x1 ?y1))
   (monster ?monster ?monster-type)
   (monsterSpeed ?monster-type ?base-speed)
   (relativeSpeed ?base-speed ?speed)
   (nStepsTowardsHero ?speed ?x1 ?y1 ?x2 ?y2))

; Meaning: Unprovoked monsters don't move
(<= (nextMonsterLocation ?monster ?x ?y)
   (monster ?monster ?monster-type)
   (not (provoked ?monster))
   (true (location ?monster ?x ?y)))

(<= (nextMonsterLocation ?monster ?x ?y)
   (monster ?monster ?monster-type)
   (provoked ?monster)                             ; Added 8/2/07 thanks to SW
   (true (invisible hero ?n))                      ; invisible for n more turns
   (true (location ?monster ?x ?y)))


; Meaning: How many steps can monster take this turn?
; We slow all monsters down when hero has enhancedSpeed
(<= (relativeSpeed 0.5 ?speed)
   (not (currentlyEnhancedSpeed hero))
   (halfSpeed ?speed))
(<= (relativeSpeed 0.5 ?speed)
   (true (enhancedSpeed hero ?n))
   (quarterSpeed ?speed))
(<= (relativeSpeed ?base-speed ?base-speed)
   (speedNumber ?base-speed)                        ; SW ****
   (distinct ?base-speed 0.5)
   (not (currentlyEnhancedSpeed hero)))
(<= (relativeSpeed 1 ?speed)
   (true (enhancedSpeed hero ?n))
   (halfSpeed ?speed))
(<= (relativeSpeed 2 1)
   (true (enhancedSpeed hero ?n)))

; Anything that moves slower than Hero is handled by
; flipping speed from 1 to 0 every other turn
; (or every fourth turn for 1/4 speed monsters)
(<= (halfSpeed 1)
   (true odd))
(<= (halfSpeed 0)
   (true even))
(<= (quarterSpeed 1)
   (true (slomo-time 4)))
(<= (quarterSpeed 0)
   (true (slomo-time ?n))
   (distinct ?n 4))

; Meaning: Conditions when health is restored via restoreHealth scroll
(<= (healthRestoredByScroll)
   (does hero (read ?scroll))
   (scroll ?scroll restoreHealth)
   (true (health hero ?health))
   (maxHealth hero ?maxHealth)
   (distinct ?health ?maxHealth))

;----------------------------------------------------------------------
; Object-interaction relations
;----------------------------------------------------------------------

; Meaning: The monster is hit by an arrow (unlimited range)
; SW: changed to two rules, for x==x and y==y, so we don't need arbitrary nCells (very expensive for us)
(<= (hitByArrow ?monster)
   (monster ?monster ?monster-type)               ; Generic monster
   (does hero (throw arrow ?dir))
   (true (location hero ?x ?y1))
   (true (location ?monster ?x ?y2)))             ; Action

(<= (hitByArrow ?monster)
   (monster ?monster ?monster-type)               ; Generic monster
   (does hero (throw arrow ?dir))
   (true (location hero ?x1 ?y))
   (true (location ?monster ?x2 ?y)))             ; Action

; Meaning: Hero adds item to sack if on it at end of time step
; - And it wasn't dropped in this time step
(<= (pickedUp ?item)
   (item ?item)                         ; Generic item
   (distinct ?item gold)
   (true (burden ?burden))
   (maxBurden ?max-burden)              ; Generic sack
   (> ?max-burden ?burden)              ; Arithmetic
   (true (location hero ?x1 ?y1))
   (nextHeroLocation ?x2 ?y2)           ; Action
   (not (sameLocation ?x1 ?y1 ?x2 ?y2)) ; Don't re-pick up an item after dropping it
   (true (location ?item ?x2 ?y2)))

; Handle gold specially since it is not limited by maxBurden
(<= (pickedUp gold)
   (nextHeroLocation ?x ?y)
   (true (location gold ?x ?y)))

; Meaning: Read scrolls and quaffed potions are eliminated from burden
(<= (decrementsBurden ?item)
   (use ?item))

; Meaning: Dropped items are elminated from burden
(<= (decrementsBurden ?item)
   (does hero (drop ?item)))

; Meaning: Picking up a non-gold item changes the burden
(<= burdenChanged
   (pickedUp ?item)            ; acquired new item
   (not (weaponChange ?item))  ; DA: No change if changing a weapon
   (distinct ?item gold))      ; No change if it's gold

; Meaning: Any decrement to the burden changes the burden
(<= burdenChanged
   (decrementsBurden ?item)            ; successful drop or use
   (distinct ?item gold))

; Meaning: This can happen when we pickup a new weapon
(<= (changeWeapon ?currentWeapon)
   (true (wielding ?currentWeapon))       ; SW: need to ground ?weapon
   (weapon ?newWeapon ?newWeapon-type)    ; DA: Sam - is this ordering ok?
   (pickedUp ?newWeapon)                  ; DA: Also new
   (distinct ?currentWeapon ?newWeapon))

; DA (7/13): Newly inserted - used in next (to prevent burden updates) and burdenChanged/0
; -- Same as changeWeapon/1, but this time the argument represents the *new* weapon
(<= (weaponChange ?newWeapon)
   (weapon ?newWeapon ?newWeapon-type)
   (pickedUp ?newWeapon)
   (true (wielding ?currentWeapon))
   (distinct ?newWeapon ?currentWeapon))

(<= arrowThrown
   (direction ?dir)
   (does hero (throw arrow ?dir)))

; Meaning: hero is invisible at the moment.
; helper predicate to avoid unbound vars in negative statement
(<= (currentlyInvisible hero)
   (true (invisible hero ?n)))

(<= (currentlyAsleep ?agent)
   (true (asleep ?agent ?n)))

(<= (currentlyEnhancedArmor hero)
   (true (enhancedArmor hero ?n)))

(<= (currentlyEnhancedStrength hero)
   (true (enhancedStrength hero ?n)))

(<= (currentlyEnhancedSpeed hero)
   (true (enhancedSpeed hero ?n)))

(<= (armored hero)
   (true (wearing ?armor))
   (armor ?armor ?armor-type))

(<= (armed hero)
   (true (wielding ?weapon))
   (weapon ?weapon ?weapon-type))

; Meaning: Hero quaffs a potion currently in the sack
; This is just a convenience relation that indicates an item has been
; used up and no longer counts as a burden.  The effect of the
; quaff or read must be handled in a next rule.
(<= (use ?potion)
   (does hero (quaff ?potion)))

; Meaning: Hero reads a scroll currently in the sack
(<= (use ?scroll)
   (does hero (read ?scroll)))

; Meaning: The location of a provoked monster
(<= (relevantMonsterCell ?x ?y)
   (monster ?monster ?any)
   (true (location ?monster ?x ?y))
   (provoked ?monster))

; Meaning: cellNearMonster is true for any cell within manhattan
; distance 2 of a provoked monster (including the cell the monster is in)
(<= (cellNearMonster ?x ?y)
   (relevantMonsterCell ?x ?y))

(<= (cellNearMonster ?x1 ?y)
   (relevantMonsterCell ?x ?y)
   (int ?d)        ; must ground ?d
   (> ?d 0)
   (> 3 ?d)        ; There is no < (lessThan)
   (- ?x ?d ?x1))

(<= (cellNearMonster ?x1 ?y)
   (relevantMonsterCell ?x ?y)
   (int ?d)
   (> ?d 0)
   (> 3 ?d)
   (+ ?x ?d ?x1))

(<= (cellNearMonster ?x ?y1)
   (relevantMonsterCell ?x ?y)
   (int ?d)
   (> ?d 0)
   (> 3 ?d)
   (- ?y ?d ?y1))

(<= (cellNearMonster ?x ?y1)
   (relevantMonsterCell ?x ?y)
   (int ?d)
   (> ?d 0)
   (> 3 ?d)
   (+ ?y ?d ?y1))

; four diagonal cases
(<= (cellNearMonster ?x1 ?y1)
   (relevantMonsterCell ?x ?y)
   (+ ?x 1 ?x1)
   (+ ?y 1 ?y1))

(<= (cellNearMonster ?x1 ?y1)
   (relevantMonsterCell ?x ?y)
   (+ ?x 1 ?x1)
   (- ?y 1 ?y1))

(<= (cellNearMonster ?x1 ?y1)
   (relevantMonsterCell ?x ?y)
   (- ?x 1 ?x1)
   (- ?y 1 ?y1))

(<= (cellNearMonster ?x1 ?y1)
   (relevantMonsterCell ?x ?y)
   (- ?x 1 ?x1)
   (+ ?y 1 ?y1))


;;; End of file
