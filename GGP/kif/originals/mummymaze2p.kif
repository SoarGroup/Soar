;;
;; 2 player mummy maze
;;  Tyler Hicks-Wright, Eric Schkufza
;;

;; OBJECTS (Components)

(index 0)
(index 1)
(index 2)
(index 3)
(index 4)
(index 5)
(index 6)
(index 7)
(index 8)

(direction north)
(direction south)
(direction east)
(direction west)
(direction nowhere)	

(role explorer)
(role mummy)

;; INITIAL STATE (Configuration)

(init (control explorer))	

(init (location mummy 1 1))
(init (location explorer 8 1))
(init (location exit 8 8))
(init (step 0))

(init (cell 1 1))
(init (cell 1 2))
(init (cell 1 3))
(init (cell 1 4))
(init (cell 1 5))
(init (cell 1 6))
(init (cell 1 7))
(init (cell 1 8))
(init (cell 2 1))
(init (cell 2 2))
(init (cell 2 3))
(init (cell 2 4))
(init (cell 2 5))
(init (cell 2 6))
(init (cell 2 7))
(init (cell 2 8))
(init (cell 3 1))
(init (cell 3 2))
(init (cell 3 3))
(init (cell 3 4))
(init (cell 3 5))
(init (cell 3 6))
(init (cell 3 7))
(init (cell 3 8))
(init (cell 4 1))
(init (cell 4 2))
(init (cell 4 3))
(init (cell 4 4))
(init (cell 4 5))
(init (cell 4 6))
(init (cell 4 7))
(init (cell 4 8))
(init (cell 5 1))
(init (cell 5 2))
(init (cell 5 3))
(init (cell 5 4))
(init (cell 5 5))
(init (cell 5 6))
(init (cell 5 7))
(init (cell 5 8))
(init (cell 6 1))
(init (cell 6 2))
(init (cell 6 3))
(init (cell 6 4))
(init (cell 6 5))
(init (cell 6 6))
(init (cell 6 7))
(init (cell 6 8))
(init (cell 7 1))
(init (cell 7 2))
(init (cell 7 3))
(init (cell 7 4))
(init (cell 7 5))
(init (cell 7 6))
(init (cell 7 7))
(init (cell 7 8))
(init (cell 8 1))
(init (cell 8 2))
(init (cell 8 3))
(init (cell 8 4))
(init (cell 8 5))
(init (cell 8 6))
(init (cell 8 7))
(init (cell 8 8))

(init (wall 4 1 east))
(init (wall 4 1 north))
(init (wall 1 2 north))
(init (wall 2 2 north))
(init (wall 1 3 east))
(init (wall 3 3 east))
(init (wall 2 4 east))
(init (wall 3 4 east))
(init (wall 3 5 east))
(init (wall 3 5 north))
(init (wall 2 5 north))
(init (wall 2 6 east))
(init (wall 3 6 north))
(init (wall 2 8 east))
(init (wall 4 6 north))
(init (wall 4 7 east))
(init (wall 5 7 east))
(init (wall 4 4 east))
(init (wall 5 4 east))
(init (wall 8 7 north))
(init (wall 6 1 north))
(init (wall 6 2 north))
(init (wall 6 2 east))
(init (wall 6 3 east))
(init (wall 6 4 east))
(init (wall 6 6 north))
(init (wall 7 4 north))
(init (wall 7 3 east))

(init (wall 1 0 north))
(init (wall 2 0 north))
(init (wall 3 0 north))
(init (wall 4 0 north))
(init (wall 5 0 north))
(init (wall 6 0 north))
(init (wall 7 0 north))
(init (wall 8 0 north))
(init (wall 1 8 north))
(init (wall 2 8 north))
(init (wall 3 8 north))
(init (wall 4 8 north))
(init (wall 5 8 north))
(init (wall 6 8 north))
(init (wall 7 8 north))
(init (wall 8 8 north))
(init (wall 0 1 east))
(init (wall 0 2 east))
(init (wall 0 3 east))
(init (wall 0 4 east))
(init (wall 0 5 east))
(init (wall 0 6 east))
(init (wall 0 7 east))
(init (wall 0 8 east))
(init (wall 8 1 east))
(init (wall 8 2 east))
(init (wall 8 3 east))
(init (wall 8 4 east))
(init (wall 8 5 east))
(init (wall 8 6 east))
(init (wall 8 7 east))
(init (wall 8 8 east))

;; ACTIONS

(<= (legal explorer (move1 ?dir))
 	(direction ?dir)
	(true (control explorer)))
(<= (legal explorer (move1 nowhere))
	(true (control mummy))) 
(<= (legal mummy (move2 ?dir1 ?dir2))
	(direction ?dir1)
    (direction ?dir2)
	(true (control mummy)))
(<= (legal mummy (move2 nowhere nowhere))
 	(true (control explorer)))

;; DYNAMICS

(<= (next (location explorer ?x2 ?y2))
 	(true (location explorer ?x1 ?y1))
	(does explorer (move1 ?dir))
	(newlocation1 ?x1 ?y1 ?dir ?x2 ?y2))
(<= (next (location mummy ?x2 ?y2))
	(true (location mummy ?x1 ?y1))
   	(does mummy (move2 ?dir1 ?dir2))
	(newlocation2 ?x1 ?y1 ?dir1 ?dir2 ?x2 ?y2))	
	
;; FRAME AXIOMS

(<= (next (location exit ?x ?y))
 	(true (location exit ?x ?y)))
(<= (next (cell ?x ?y))
 	(true (cell ?x ?y)))
(<= (next (wall ?x ?y ?z))
	(true (wall ?x ?y ?z))) 

;; STEP AXIOMS

(<= (next (step ?x))
	(true (step ?y))
	(succ ?y ?x))
	
;; CONTROL AXIOMS

(<= (next (control explorer))
 	(true (control mummy)))
(<= (next (control mummy))
 	(true (control explorer)))

;; VIEW RELATIONS

(<= eaten
 	(true (location explorer ?x ?y))
	(true (location mummy ?x ?y)))
(<= atexit
 	(true (location explorer ?x ?y))
	(true (location exit ?x ?y)))
(<= laststep
 	(true (step 50)))

;; COMPUTATIONAL RELATIONS
(<= (newlocation1 ?x ?y nowhere ?x ?y)
 	(index ?x)
	(index ?y))
	
(<= (newlocation1 ?x ?y north ?x ?y)
	(true (wall ?x ?y north)))
(<= (newlocation1 ?x ?y south ?x ?y)
 	(succ ?down ?y)
	(true (wall ?x ?down north)))
(<= (newlocation1 ?x ?y east ?x ?y)
	(true (wall ?x ?y east)))
(<= (newlocation1 ?x ?y west ?x ?y)
	(succ ?left ?x)
	(true (wall ?left ?y east)))

(<= (newlocation1 ?x ?y1 north ?x ?y2)
	(succ ?y1 ?y2)
	(index ?x)
	(not (true (wall ?x ?y1 north))))
(<= (newlocation1 ?x ?y1 south ?x ?y2)
	(succ ?y2 ?y1)
	(index ?x)
	(not (true (wall ?x ?y2 north))))
(<= (newlocation1 ?x1 ?y east ?x2 ?y)
	(succ ?x1 ?x2)
	(index ?y)
	(not (true (wall ?x1 ?y east))))
(<= (newlocation1 ?x1 ?y west ?x2 ?y)
	(succ ?x2 ?x1)
	(index ?y)
	(not (true (wall ?x2 ?y east))))

(<= (newlocation2 ?x1 ?y1 ?dir1 ?dir2 ?x3 ?y3)
	(newlocation1 ?x1 ?y1 ?dir1 ?x2 ?y2)
   	(newlocation1 ?x2 ?y2 ?dir2 ?x3 ?y3))	
	
;; STATIC RELATIONS

(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)

;; TERMINAL CONDITIONS

(<= terminal laststep)
(<= terminal eaten)
(<= terminal atexit)

;; GOAL VALUES
(<= (goal explorer 100)
 	atexit
	(not eaten))
(<= (goal explorer 50)
	(not atexit)
   	(not eaten))
(<= (goal explorer 0)
	eaten)

(<= (goal mummy 0)
 	atexit
	(not eaten))
(<= (goal mummy 50)
	(not atexit)
   	(not eaten))
(<= (goal mummy 100)
	eaten) 
