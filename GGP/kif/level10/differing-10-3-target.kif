;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Wargame Template (August 2007)
;;;  - Matthew C. Molineaux (adapted from files provided by SLG & U.Michigan)
;;;  - TL Level 10, Scenario 3, Target task
;;;	 - Replaces original scenario 3 based on transferrence of reachability.
;;;  - Uses built-in math functions
;;;  - Based on TL level 10, Scenario 1, Source task
;;;
;;;  - 8/30: MCM: Created
;;;  - 9/14: MCM: Updated to revised core rules for differing scenarios
;;; 
;;; Note: The goal of this scenario is to reach the exit. That goal cannot be
;;; completed without shooting the terrorist. Shooting the terrorist is not 
;;; specifically enumerated as a goal.
;;;
;;; - One solution (length 14, verified): WW<shoot s>EEENNWWWWSS 
;;;
;;; Debugger: "http://games.stanford.edu:5999/director/debugrawgame?"
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Map (and Legend)
;                        
;    1 2 3 4 5 6 7 8     
;   +---------------+    + = Corner
;  1|. . . . . . . .|    . = A room location
;   |       +       |    - = Horizontal wall
;  2|. . . .|. . . .|    | = Vertical wall
;   | +-----+       |
;  3|F|G . @ . . . .|    @ = Soldier
;   | +-------------|    T = Terrorist
;  4|. . . . . . . .|    F = Flag (exit)
;   |         +-----|    G = Gun
;  5|. T . . . . . .|    R = gRenade
;   |         +     |
;  6|. . . . .|. . .|    
;   +---------------+    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role soldier)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT (and some type) Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
;;; Specifies where objects start on board, where lower left is (1,1).
;;; Object locations
(init (location soldier 4 3))
(init (location terrorist1 2 5))
(init (location gun1 2 3))
(init (location exit 1 3))
(init (location flag 1 3))

;; Dynamic type hierarchies
(type gun1 item) 
(type gun1 losItem) 
(type terrorist1 animate)
(type terrorist1 terrorist)
(type terrorist1 horizontalTerrorist)

;;; Character health
(init (health terrorist1 1))

;; Test for absence of blockedEast, blockedSouth
(blockedEast)
(blockedSouth)

;;; Unless otherwise specified adjacent spatial coordinates are assumed 
;;;  accessible from one another.  
;;Outer walls

(init (blocked west 1 1))
(init (blocked west 1 2))
(init (blocked west 1 3))
(init (blocked west 1 4))
(init (blocked west 1 5))
(init (blocked west 1 6))

(init (blocked east 8 1))
(init (blocked east 8 2))
(init (blocked east 8 3))
(init (blocked east 8 4))
(init (blocked east 8 5))
(init (blocked east 8 6))

(init (blocked north 1 1))
(init (blocked north 2 1))
(init (blocked north 3 1))
(init (blocked north 4 1))
(init (blocked north 5 1))
(init (blocked north 6 1))
(init (blocked north 7 1))
(init (blocked north 8 1))

(init (blocked south 1 6))
(init (blocked south 2 6))
(init (blocked south 3 6))
(init (blocked south 4 6))
(init (blocked south 5 6))
(init (blocked south 6 6))
(init (blocked south 7 6))
(init (blocked south 8 6))

;;Inner walls
(init (blocked east 4 2))
(init (blocked east 1 3))
(init (blocked east 5 6))

(init (blocked west 5 2))
(init (blocked west 2 3))
(init (blocked west 6 6))

(init (blocked north 2 3))
(init (blocked north 3 3))
(init (blocked north 4 3))
(init (blocked north 2 4))
(init (blocked north 3 4))
(init (blocked north 4 4))
(init (blocked north 6 5))
(init (blocked north 7 5))
(init (blocked north 8 5))
(init (blocked north 5 4))
(init (blocked north 6 4))
(init (blocked north 7 4))
(init (blocked north 8 4))

(init (blocked south 2 2))
(init (blocked south 3 2))
(init (blocked south 4 2))
(init (blocked south 2 3))
(init (blocked south 3 3))
(init (blocked south 4 3))
(init (blocked south 6 4))
(init (blocked south 7 4))
(init (blocked south 8 4))
(init (blocked south 5 3))
(init (blocked south 6 3))
(init (blocked south 7 3))
(init (blocked south 8 3))

;;;<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
; MCM 8/14/07: Blocked is now directly represented in state.
; SW 8/3/07: need these to have consistent spatial rep. with the other domains
; Note that south and north are reversed compared to rogue/escape.

;; Soldier health
(init (health soldier 1))

;; Static type hierarchy
; MCM (9/14): Exit is a place, now.
(type exit place)

;;; The global time clock.	
(init (time 1))
	

;;MCM (9/14): Moved the Goal section to be more easily separable from domain
;;            rules.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; Goals are computed according to the following table:
;;;
;;; Dead? TimeOut? atExit? Goal
;;; ----------------------------
;;;    0        0        0     0   
;;;    0        0        1   100
;;;    0        1        0    50 
;;;    0        1        1   100
;;;    1        0        0     0
;;;    1        0        1     0
;;;    1        1        0     0
;;;    1        1        1     0

(<= (goal soldier 0)
    (dead soldier))

(<= (goal soldier 0)
    (not (dead soldier))
    (not timeOut)
    (not atExit))

(<= (goal soldier 50)
    (not (dead soldier))
    timeOut
	(not atExit))

(<= (goal soldier 100)
    (not (dead soldier))
    atExit)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;Rules that change across scenarios
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; MCM (9/14): healingItems get used up in the heal action, in some scenarios.
;;; DA (8/31): (heal ?item)
(<= (uses ?item) ;; MCM (9/6): Modified arguments to make more similar
    (type ?item healingItem)
    (does soldier (heal ?item)))

;;; MCM (9/14): In some scenarios, healingItems heal 1 point; in others, 2.
;;; DA (8/31): (heal ?item)
(<= (healthIncrement soldier 1)
    soldierHeals)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; It is always legal for the soldier to stand still.

(<= (legal soldier noop))

;;; The soldier may step in any of the cardinal directions so long as a wall 
;;;  does not block the way.

(<= (legal soldier (move ?dir))
    (true (location soldier ?x ?y))
    (direction ?dir)
    (not (stepBlocked ?dir ?x ?y))
    (nextCell ?dir ?x ?y ?xAny ?yAny))

;MCM: Added nextCell so that wargame move looks like rogue move
;MCM (9/4): Corrected parameter names
(<= (nextCell ?dir ?x1 ?y1 ?x2 ?y2)
    (nStepsSoldier 1 ?dir ?x1 ?y1 ?x2 ?y2))

;;; The soldier may heal himself if he is holding a healing type item.
;;; DA (8/31): Added ?item parameter to heal
;;; MCM (8/31): Changed (holding soldier ?item) to (carrying ?item)
(<= (legal soldier (heal ?item))
    (true (carrying ?item))
    (type ?item healingItem))

;;; The soldier may shoot in any of the cardinal directions if he is holding
;;;  a line-of-sight type weapon.
;;; MCM (8/31): Updated from shooting in a direction to shooting a monster
;;; MCM (8/31): Changed (holding soldier ?item) to (carrying ?item)
(<= (legal soldier (shoot ?terrorist))
    (true (carrying ?item))
    (type ?item losItem)
	(canShootAt ?terrorist))                       ; Generic spatial

;;; The soldier may throw in any of the cardinal directions if he is holding
;;;  a area-of-effect type item.
;;; MCM (8/31): Changed (holding soldier ?item) to (carrying ?item)
(<= (legal soldier (throw ?dir ?dist))
    (true (carrying ?item))
    (type ?item aoeItem)
    (legalThrowDist ?dist)
    (true (location soldier ?x ?y))
    (nStepsSoldier ?dist ?dir ?x ?y ?xAny ?yAny))
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; MCM: blockedSouth and blockedEast are now removed, replaced by only blocked
;;; in the state.
(<= (next (blocked ?dir ?x ?y))
    (true (blocked ?dir ?x ?y)))

; SW 8/3/07
(<= (next (location soldier ?x ?y))
    (intendedSoldierLocation ?x ?y))

;;; Location for animate-type objects is updated by the intendedLocation axiom.
(<= (next (location ?character ?x ?y))
    (type ?character animate)
    (intendedLocation ?character ?x ?y))

;MCM: Locations of places don't change.
(<= (next (location ?place ?x ?y))
    (type ?place place)
    (true (location ?place ?x ?y)))

;;; Item-type objects remain where they are until the soldier walks on top of
;;;  them.
; Soar change: splitting to two cases, so we don't need distinctCell.
; MCM (9/4): Added intermediate "taken" to make look more
; like mRogue.
; MCM (9/4): Fixed by changing ?object to ?item
(<= (next (location ?item ?xO ?yO))
    (type ?item item)
    (true (location ?item ?xO ?yO))
    (not (taken ?item)))

;(<= (next (location ?object ?xO ?yO))
;    (type ?object item)
;    (true (location ?object ?xO ?yO))
;    (intendedSoldierLocation ?xE ?yE)
;    (distinct ?xE ?xO))
;    ;(distinctCell ?xE ?yE ?xO ?yO))
;
;(<= (next (location ?object ?xO ?yO))
;    (type ?object item)
;    (true (location ?object ?xO ?yO))
;    (intendedSoldierLocation ?xE ?yE)
;    (distinct ?yE ?yO))

;;; Should the soldier walk on top of an item-type object, it will move into his
;;;  inventory where it will remain until it is used.
;;; MCM (8/31): Changed (holding soldier ?item) to (carrying ?item)
;;; MCM (9/4) : Added "taken" intermediary to make more like mRogue.
(<= (next (carrying ?object))
    (type ?object item)
    (taken ?object))

;(<= (next (carrying ?object))
;    (type ?object item)
;    (true (location ?object ?xO ?yO))
;    (intendedSoldierLocation ?xO ?yO)) ; Soar change: this is much more efficient
;   ; (intendedSoldierLocation ?xE ?yE)
;   ; (not (distinctCell ?xE ?yE ?xO ?yO)))

(<= (next (carrying ?item))
    (true (carrying ?item))
    (not (uses ?item))) ;; MCM (9/6): Modified arguments to make more similar

;;; Character health is modified based on how much it is 
;;;  [incremented,decremented] per step.
(<= (next (health ?character ?h2))
    (true (health ?character ?h1))
    (healthIncrement ?character ?hInc)
    (healthDecrement ?character ?hDec)
    (- ?hInc ?hDec ?deltaH)
    (+ ?h1 ?deltaH ?h2))
	
;;; Time ticks one second per step.	
(<= (next (time ?tNext))
    (true (time ?tCurrent))
    (+ ?tCurrent 1 ?tNext))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; The game is over if time runs out, the soldier has his health reduced to
;;;  zero, or reaches the exit
(<= terminal
    timeOut)
(<= terminal
    (dead soldier))
(<= terminal
    atExit)

;;; DA (8/31): Added to be analogous with mRogue's atExit/0
(<= atExit
    (index ?x)
    (index ?y)
    (true (location soldier ?x ?y))
    (true (location exit ?x ?y)))
 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; MCM (9/4): Added taken to correspond with mRogue pickedUp.
(<= (taken ?item)
    (type ?item item)
    (true (location ?item ?x ?y))
    (intendedSoldierLocation ?x ?y))

;;; A single step ?dir from cell (?x,?y) is blocked if a wall interupts that 
;;;  step.

(<= (stepBlocked ?dir ?x ?y)
    (true (blocked ?dir ?x ?y)))

;;; A path of an arbitrary number ?n steps ?dir from cell (?x, ?y) is blocked 
;;;  if a wall interupts any of the steps along that path.

; changed for Soar: split to four cases to ease matching
; also, these can be restricted to blocked from the soldiers location, to get rid of useless groundings
; in addition, there is no need to infer unless the soldier is shooting
(<= (pathBlocked west ?x ?y1 ?x ?y3)
    (does soldier (shoot ?terrorist))
    (true (location soldier ?x ?y1))
    (index ?n)
    (nStepsSoldier ?n west ?x ?y1 ?x ?y3)
    (index ?m)
    (> ?n ?m)
    (nStepsSoldier ?m west ?x ?y1 ?x ?y2)
    (stepBlocked west ?x ?y2))    

(<= (pathBlocked south ?x1 ?y ?x3 ?y)
    (does soldier (shoot ?terrorist))
    (true (location soldier ?x1 ?y))
    (index ?n)
    (nStepsSoldier ?n south ?x1 ?y ?x3 ?y)
    (index ?m)
    (> ?n ?m)
    (nStepsSoldier ?m south ?x1 ?y ?x2 ?y)
    (stepBlocked south ?x2 ?y))    
(<= (pathBlocked east ?x ?y1 ?x ?y3)
    (does soldier (shoot ?terrorist))
    (true (location soldier ?x ?y1))
    (index ?n)
    (nStepsSoldier ?n east ?x ?y1 ?x ?y3)
    (index ?m)
    (> ?n ?m)
    (nStepsSoldier ?m east ?x ?y1 ?x ?y2)
    (stepBlocked east ?x ?y2))    

(<= (pathBlocked north ?x1 ?y ?x3 ?y)
    (does soldier (shoot ?terrorist))
    (true (location soldier ?x1 ?y))
    (index ?n)
    (nStepsSoldier ?n north ?x1 ?y ?x3 ?y)
    (index ?m)
    (> ?n ?m)
    (nStepsSoldier ?m north ?x1 ?y ?x2 ?y)
    (stepBlocked north ?x2 ?y))    

;;; nSteps computes the cell found ?n steps ?dir of cell (?x,?y) within the
;;;  confines of an 8 x 8 grid.
; Soar: this is only used when ?n == 1 (since many cases are now nStepsSoldier), so we can get rid of all other groundings
(<= (nSteps 1 south ?x ?y1 ?x ?y2)
    (cell ?x ?y1)
    (+ ?y1 1 ?y2)
    (index ?y2))
(<= (nSteps 1 north ?x ?y1 ?x ?y2)	
    (cell ?x ?y1)
    (- ?y1 1 ?y2)
    (index ?y2))
(<= (nSteps 1 east ?x1 ?y ?x2 ?y)
    (cell ?x1 ?y)
    (+ ?x1 1 ?x2)
    (index ?x2))
(<= (nSteps 1 west ?x1 ?y ?x2 ?y)
    (cell ?x1 ?y)
    (- ?x1 1 ?x2)
    (index ?x2))

; for Soar: the soldier is commonly the first coords in an nStep match, this restricted version is easier to handle 
(<= (nStepsSoldier ?n south ?x ?y1 ?x ?y2)
    (index ?n) ; SW 8/3/07
    (true (location soldier ?x ?y1))
    (+ ?y1 ?n ?y2)
    (index ?y2))
(<= (nStepsSoldier ?n north ?x ?y1 ?x ?y2)
    (index ?n) ; SW 8/3/07
    (index ?x)
    (true (location soldier ?x ?y1))
    (- ?y1 ?n ?y2)
    (index ?y2))
(<= (nStepsSoldier ?n east ?x1 ?y ?x2 ?y)
    (index ?n) ; SW 8/3/07
    (index ?y)
    (true (location soldier ?x1 ?y))
    (+ ?x1 ?n ?x2)
    (index ?x2))
(<= (nStepsSoldier ?n west ?x1 ?y ?x2 ?y)
    (index ?n) ; SW 8/3/07
    (index ?y)
    (true (location soldier ?x1 ?y))
    (- ?x1 ?n ?x2)
    (index ?x2))

;;; Two cells are vertically related [south,north] of each other if the first is
;;;  [higher,lower] than the second.
(<= (verticalRelation ?terrorist south ?x1 ?y1 ?x2 ?y2)
    (cellNearTerrorist ?terrorist ?x2 ?y2) ; Soar
    (intendedSoldierLocation ?x1 ?y1) ; Soar
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (> ?y1 ?y2))
(<= (verticalRelation ?terrorist north ?x1 ?y1 ?x2 ?y2)
    (cellNearTerrorist ?terrorist ?x2 ?y2) ; Soar
    (intendedSoldierLocation ?x1 ?y1) ; Soar
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (> ?y2 ?y1))

;;; Two cells are horizontall related [east,west] of each other if the first is
;;;  [right,left] of the second.
(<= (horizontalRelation ?terrorist east ?x1 ?y1 ?x2 ?y2)
    (cellNearTerrorist ?terrorist ?x2 ?y2) ; Soar
    (intendedSoldierLocation ?x1 ?y1) ; Soar
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (> ?x1 ?x2))
(<= (horizontalRelation ?terrorist west ?x1 ?y1 ?x2 ?y2)
    (cellNearTerrorist ?terrorist ?x2 ?y2) ; Soar
    (intendedSoldierLocation ?x1 ?y1) ; Soar
    (cell ?x1 ?y1)
    (cell ?x2 ?y2)
    (> ?x2 ?x1))	
	
;;; Defines the legal distances that an object may be thrown (0, 1, or 2 cells)
(<= (legalThrowDist 2))

;;; Every item is used if the soldier performs the action that it enables.
(<= (uses ?item) ;; MCM (9/6): Modified arguments to make more similar
    (type ?item losItem)
    (does soldier (shoot ?terrorist)))
(<= (uses ?item) ;; MCM (9/6): Modified arguments to make more similar
    (type ?item aoeItem)
    (does soldier (throw ?dir ?dist)))

;; MCM: Removed logic for finding enemies in LoS; now, if a soldier shoots someone, 
;; the square that someone is on gets attacked.
(<= (attacks ?x ?y soldier)
    (does soldier (shoot ?terrorist))
    (intendedLocation ?terrorist ?x ?y))

;;MCM: Old attacks logic
;;; Cell (?x,?y) is attacked by the soldier if the soldier performs the shoot
;;;  action and that cell is within a line of sight ?dir of the soldier.

;(<= (attacks ?x ?y soldier)
;    (does soldier (shoot ?dir))
;    (true (location soldier ?x ?y)))
;(<= (attacks ?x2 ?y2 soldier)
;    (does soldier (shoot ?dir))
;    (true (location soldier ?x1 ?y1))
;    (index ?n)
;    (nStepsSoldier ?n ?dir ?x1 ?y1 ?x2 ?y2)
;    (not (pathBlocked ?dir ?x1 ?y1 ?x2 ?y2)))


;; canShootAt added to make parameterization for (shoot ?terrorist) work.
(<= (canShootAt ?terrorist)
    (type ?terrorist terrorist)
    (true (location soldier ?x1 ?y1))
    (true (location ?terrorist ?x2 ?y2))  ;;MCM (9/4): Changed from "intendedLocation"
    (index ?n)
    (direction ?dir)
    (nStepsSoldier ?n ?dir ?x1 ?y1 ?x2 ?y2))
    
;;; Cell (?x,?y) is attacked by the soldier if the soldier performs the throw
;;;  action and that cell is within a one cell radius of where the item thrown
;;;  by the soldier lands.
(<= (attacks ?x2 ?y2 soldier)
    (does soldier (throw ?dir ?dist))
    (true (location soldier ?x1 ?y1))
    (nStepsSoldier ?dist ?dir ?x1 ?y1 ?x2 ?y2))
(<= (attacks ?x3 ?y3 soldier)
    (does soldier (throw ?dir ?dist))
    (true (location soldier ?x1 ?y1))
    (nStepsSoldier ?dist ?dir ?x1 ?y1 ?x2 ?y2)
    (nSteps 1 ?dirAny ?x2 ?y2 ?x3 ?y3))

;;; Any character of type terrorist attacks a cell (?x,?y) if that character
;;;  will be standing there at the beginning of the next time-step.
(<= (attacks ?x ?y ?terrorist)
    (type ?terrorist terrorist)
    (intendedLocation ?terrorist ?x ?y))

;;; A terrorist attacks a cell (?x,?y) if any character of type terrorist 
;;;  attacks that cell.
(<= (terroristAttacks ?x ?y)
    (type ?terrorist terrorist)
    (attacks ?x ?y ?terrorist))

;;; At the beginning of the next time-step the soldier will either be one step
;;;  away if he performed the step action, or in the same place if he performed
;;;  some other action.
(<= (intendedSoldierLocation ?x2 ?y2)
    (true (location soldier ?x1 ?y1))
    (does soldier (move ?dir)) ; Soar change: 'step' action needs to be 'move' for spatial assumptions  
    (nextCell ?dir ?x1 ?y1 ?x2 ?y2))
(<= (intendedSoldierLocation ?x ?y)
    (true (location soldier ?x ?y))
    (not (does soldier (move south)))
    (not (does soldier (move north)))
    (not (does soldier (move east)))
    (not (does soldier (move west))))    

;;; A dead terrorist is incapacitated and will not move.
(<= (intendedLocation ?terrorist ?x ?y)
    (true (location ?terrorist ?x ?y))
    (type ?terrorist terrorist)
    (dead ?terrorist))
    
;;; A character of type [horizontal,vertical]-terrorist will perform two
;;; [horizontal,vertical] steps per turn.
(<= (intendedLocation ?terrorist ?x3 ?y3)      ; xMumNew, yMumNew
    (type ?terrorist horizontalTerrorist)
    (not (dead ?terrorist))
    (intendedSoldierLocation ?xS ?yS)         ; ?xExp, ?yExp
    (true (location ?terrorist ?x1 ?y1))       ; ?xMum, ?yMum
    (horizontalStep ?terrorist ?x1 ?y1 ?x2 ?y2 ?xS ?yS)   
    (horizontalStep ?terrorist ?x2 ?y2 ?x3 ?y3 ?xS ?yS))
(<= (intendedLocation ?terrorist ?x3 ?y3)
    (type ?terrorist verticalTerrorist)
    (not (dead ?terrorist))
    (intendedSoldierLocation ?xS ?yS)
    (true (location ?terrorist ?x1 ?y1))
    (verticalStep ?terrorist ?x1 ?y1 ?x2 ?y2 ?xS ?yS)
    (verticalStep ?terrorist ?x2 ?y2 ?x3 ?y3 ?xS ?yS))

;;; A [horizontal,vertical] step is one that will move [horizontally,vertically]
;;;  toward the intended location of the soldier before it will move 
;;;  [vertically,horizontally].
(<= (horizontalStep ?terrorist ?x1 ?y1 ?x2 ?y2 ?xT ?yT)
    (wouldStepHorizontal ?terrorist ?dir ?x1 ?y1 ?xT ?yT)
    (nSteps 1 ?dir ?x1 ?y1 ?x2 ?y2))
(<= (horizontalStep ?terrorist ?x1 ?y1 ?x2 ?y2 ?xT ?yT)
    (wouldStepVertical ?terrorist ?dir ?x1 ?y1 ?xT ?yT)
    (nSteps 1 ?dir ?x1 ?y1 ?x2 ?y2)
    (not (wouldStepHorizontal ?terrorist east ?x1 ?y1 ?xT ?yT))
    (not (wouldStepHorizontal ?terrorist west ?x1 ?y1 ?xT ?yT)))
(<= (horizontalStep ?terrorist ?x ?y ?x ?y ?xT ?yT)
    (cell ?x ?y)
    (cell ?xT ?yT)
    (cellNearTerrorist ?terrorist ?x ?y) ; Soar
    (intendedSoldierLocation ?xT ?yT) ; Soar
    (not (wouldStepHorizontal ?terrorist east ?x ?y ?xT ?yT))
    (not (wouldStepHorizontal ?terrorist west ?x ?y ?xT ?yT))
    (not (wouldStepVertical ?terrorist south ?x ?y ?xT ?yT))
    (not (wouldStepVertical ?terrorist north ?x ?y ?xT ?yT)))
    
(<= (verticalStep ?terrorist ?x1 ?y1 ?x2 ?y2 ?xT ?yT)
    (wouldStepVertical ?terrorist ?dir ?x1 ?y1 ?xT ?yT)
    (nSteps 1 ?dir ?x1 ?y1 ?x2 ?y2))
(<= (verticalStep ?terrorist ?x1 ?y1 ?x2 ?y2 ?xT ?yT)
    (wouldStepHorizontal ?terrorist ?dir ?x1 ?y1 ?xT ?yT)
    (nSteps 1 ?dir ?x1 ?y1 ?x2 ?y2)
    (not (wouldStepVertical ?terrorist south ?x1 ?y1 ?xT ?yT))
    (not (wouldStepVertical ?terrorist north ?x1 ?y1 ?xT ?yT)))
(<= (verticalStep ?terrorist ?x ?y ?x ?y ?xT ?yT)
    (cell ?x ?y)
    (cell ?xT ?yT)
    (cellNearTerrorist ?terrorist ?x ?y) ; Soar
    (intendedSoldierLocation ?xT ?yT) ; Soar
    (not (wouldStepVertical ?terrorist south ?x ?y ?xT ?yT))
    (not (wouldStepVertical ?terrorist north ?x ?y ?xT ?yT))
    (not (wouldStepHorizontal ?terrorist east ?x ?y ?xT ?yT))
    (not (wouldStepHorizontal ?terrorist west ?x ?y ?xT ?yT)))
    
;;; True if the step from (?x1,?y1) toward (?x2,?y2) along the direction ?dir
;;;  is not blocked by a wall.
(<= (wouldStepHorizontal ?terrorist ?dir ?x1 ?y1 ?x2 ?y2)
    (cellNearTerrorist ?terrorist ?x1 ?y1) ; Soar
    (intendedSoldierLocation ?x2 ?y2) ; Soar
    (horizontalRelation ?terrorist ?dir ?x2 ?y2 ?x1 ?y1)
    (not (stepBlocked ?dir ?x1 ?y1)))
(<= (wouldStepVertical ?terrorist ?dir ?x1 ?y1 ?x2 ?y2)
    (cellNearTerrorist ?terrorist ?x1 ?y1) ; Soar
    (intendedSoldierLocation ?x2 ?y2) ; Soar
    (verticalRelation ?terrorist ?dir ?x2 ?y2 ?x1 ?y1)
    (not (stepBlocked ?dir ?x1 ?y1)))	

;;; The soldier's health is incremented by 0 unless a healing item is used.
;;; DA (8/31): (heal ?item)
(<= (healthIncrement soldier 0) 
	(not soldierHeals))

;MCM (9/14): Added to resolve bug with healing when more than one item in 
;            scenario.
(<= soldierHeals
    (type ?item item)
    (does soldier (heal ?item)))
    
;;; The soldier's health is decremented by 1 if he is attacked by a terrorist,
;;;  0 otherwise.
(<= (healthDecrement soldier 1)
    (intendedSoldierLocation ?x ?y)
    (terroristAttacks ?x ?y))
(<= (healthDecrement soldier 0)
    (intendedSoldierLocation ?x ?y)
    (not (terroristAttacks ?x ?y)))

;;; A terrorist's health may never be incremented
(<= (healthIncrement ?terrorist 0)
    (type ?terrorist terrorist))
    
;;; A terrorist's health is decremented by 1 if he is attacked by the soldier,
;;;  0 otherwise.
(<= (healthDecrement ?terrorist 1)
    (type ?terrorist terrorist)
    (terroristAttacked ?terrorist))
    
(<= (healthDecrement ?terrorist 0)
    (type ?terrorist terrorist)
    (not (terroristAttacked ?terrorist)))
    
(<= (terroristAttacked ?terrorist)
    (intendedLocation ?terrorist ?x ?y)
    (attacks ?x ?y soldier))

;;; A character with 0 health is dead.
(<= (dead ?object)
    (true (health ?object 0)))

;;; A cell is composed of two indices in the range [1,8]
(<= (cell ?x ?y)
    (index ?x)
    (index ?y))

;;; Timeout occurs at time t=timeFinal
(<= timeout
    (true (time ?t))
    (timeFinal ?t))

(<= shotTerrorist
    (type ?terrorist terrorist)
    (dead ?terrorist))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations (i.e., constants)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 

;;; Indices in the range [1,8].
(index 1) (index 2) (index 3) (index 4) (index 5) (index 6) (index 7) (index 8)

;;; The cardinal directions {south,north,east,west}.
(direction south) (direction north) (direction east) (direction west)

;;; The time at which the game will end.
(timeFinal 50)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Lower-level map relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 

; Soar: constrain the terrorist movement predicates to only match near terrorists
; This means all cells within manhattan distance 2 of a terrorist
(<= (terroristCell ?terrorist ?x ?y)
    (type ?terrorist terrorist) 
    (true (location ?terrorist ?x ?y)))

(<= (cellNearTerrorist ?terrorist ?x ?y)
    (terroristCell ?terrorist ?x ?y))

(<= (cellNearTerrorist ?terrorist ?x1 ?y)
    (terroristCell ?terrorist ?x ?y)
    (index ?d) ; SW 8/3/07
    (- ?x ?d ?x1)
    (> ?d 0)
    (> 3 ?d))

(<= (cellNearTerrorist ?terrorist ?x1 ?y)
    (terroristCell ?terrorist ?x ?y)
    (index ?d) ; SW 8/3/07
    (+ ?x ?d ?x1)
    (> ?d 0)
    (> 3 ?d))

(<= (cellNearTerrorist ?terrorist ?x ?y1)
    (terroristCell ?terrorist ?x ?y)
    (index ?d) ; SW 8/3/07
    (- ?y ?d ?y1)
    (> ?d 0)
    (> 3 ?d))

(<= (cellNearTerrorist ?terrorist ?x ?y1)
    (terroristCell ?terrorist ?x ?y)
    (index ?d) ; SW 8/3/07
    (+ ?y ?d ?y1)
    (> ?d 0)
    (> 3 ?d))

; four diagonal cases
(<= (cellNearTerrorist ?terrorist ?x1 ?y1) 
    (terroristCell ?terrorist ?x ?y)
    (+ ?x 1 ?x1)
    (+ ?y 1 ?y1))

(<= (cellNearTerrorist ?terrorist ?x1 ?y1) 
    (terroristCell ?terrorist ?x ?y)
    (+ ?x 1 ?x1)
    (- ?y 1 ?y1))

(<= (cellNearTerrorist ?terrorist ?x1 ?y1) 
    (terroristCell ?terrorist ?x ?y)
    (- ?x 1 ?x1)
    (- ?y 1 ?y1))

(<= (cellNearTerrorist ?terrorist ?x1 ?y1) 
    (terroristCell ?terrorist ?x ?y)
    (- ?x 1 ?x1)
    (+ ?y 1 ?y1))
