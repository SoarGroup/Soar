sp {scout*propose*hide
   (state <s> ^name scout
              ^top-state.entities.entity.area <a>
              ^waypoints.at <w>)
   (<w> ^hide <wh>)
   (<wh> ^area <a>)
-->
   (<s> ^operator <op> +)
   (<op> ^name hide
         ^waypoint <wh>)
}

sp {scout*propose*hide*switch-hiding-location
   (state <s> ^name scout
              ^top-state.entities.entity.area <a>
              ^waypoints.at <wh>)
   (<wh> ^hiding <w>)
   (<w> ^hide {<wh-o> <> <wh>})
   (<wh-o> ^area <a>)
-->
   (<s> ^operator <op> +)
   (<op> ^name hide
         ^waypoint <wh-o>)
}

sp {scout*compare*hide*better-than-rotate-to
   (state <s> ^name scout
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name hide)
   (<op2> ^name rotate-to)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {apply*hide*note-hiding
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name hide)
-->
   (<ts> ^hiding true)
   (write (crlf) |Hiding|)
   (exec player-print |Hiding|)
}

sp {apply*hide*move-to
   (state <s> ^operator <op>
              ^io.output-link <ol>
              ^parameters.throttle <thr>)
   (<op> ^name hide
         ^move-now true
         ^waypoint <h>)
   (<h> ^x <x>
        ^y <y>)
-->
   (<ol> ^move-to <mt>)
   (<mt> ^x <x>
         ^y <y>
         ^throttle <thr>)
}
