sp {go-to-location*propose*go-to-waypoint
   (state <s> ^name go-to-location
              ^top-state.go-to-location.waypoint <w>)
            # -^top-state.move-to) # not already executing a move-to command
-->
   (<s> ^operator <op> +)
   (<op> ^name go-to-waypoint
         ^destination <w>
         ^track-progress true) #create a move-to structure on state
}

# 1) create waypoint structure on input-link (done by choose-waypoint)
# 2) rotate-to to the desired heading 
# 3) create structures to track movement
# 4) move/rotate to the desired location (in subgoal)

sp {elaborate*operator*go-to-waypoint*waypoint
   (state <s> ^io.input-link.self.waypoints.waypoint <w>
              ^operator <o>)
   (<o> ^name go-to-waypoint
        ^destination.id <id>)
   (<w> ^id <id>)
-->
   (<o> ^waypoint <w>)
}

sp {apply*go-to-waypoint*create-move-to
   (state <s> ^operator <op>
              ^io.input-link <il>
              ^waypoints.at {<at> <> <d>} # don't create a move-to structure if we are already at the destination
              ^top-state <ts>)
  -(<ts> ^move-to)
   (<il> ^time.seconds <time>
         ^self.waypoints.waypoint <w>)
   (<op> ^name go-to-waypoint
         ^move-now true
         ^track-progress true
         ^destination <d>
         ^waypoint <w>)
   (<d> ^id <id>)
   (<w> ^id <id>
        ^distance <dist>)
-->
   (<ts> ^move-to <mt>)
   (<mt> ^destination <d>
         ^source <at>
          ^closest <dist>
          ^time <time>)
}

# FIXME: this should never fire because we should properly clean up when we're finished
# but I think it may be requried after the agent gives up (check that code)
sp {apply*go-to-waypoint*remove-move-to
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<ts> ^move-to <mt>)
   (<mt> ^destination <> <w>)
   (<op> ^name go-to-waypoint
         ^move-now true
         ^destination <w>)
-->
   (<ts> ^move-to <mt> -)
}

sp {apply*go-to-waypoint*arrived-at-destination*remove-go-to-location
   (state <s> ^operator <op>
              ^top-state <ts>
              ^waypoints.at <w>)
   (<ts> ^go-to-location <gtl>)
   (<gtl> ^waypoint <w>)
   (<op> ^name go-to-waypoint
         ^move-now true
         ^destination <w>)
-->
   (<ts> ^go-to-location <gtl> -)
}

sp {apply*go-to-waypoint*arrived-at-destination*remove-move-to
   (state <s> ^operator <op>
              ^top-state <ts>
              ^waypoints.at <w>)
   (<ts> ^move-to <mt>)
   (<mt> ^destination <w>)
   (<op> ^name go-to-waypoint
         ^move-now true
         ^destination <w>)
-->
   (<ts> ^move-to <mt> -)
}

sp {monitor*go-to-waypoint
   (state <s> ^operator <o>)
   (<o> ^name go-to-waypoint
        ^destination <d>)
   (<d> ^x <x> ^y <y>)
-->
   (write (crlf) |Going to (| <x> |,| <y> |)|)
}
