sp {selection*learn
   :default
   (state <s> ^name selection)
-->
   (force-learn <s>)}

sp {evaluate-operator*elaborate*forward*visited
   (state <s> ^name selection
              ^superstate.superstate.operator.evaluation.visited <ewp>
              ^operator <op>)
   (<ewp> ^id <id>)
   (<op> ^name evaluate-operator
         ^evaluation <e>)
-->
   (write (crlf) |Visited: | <id>)
   (<e> ^visited <ewp>)
}

sp {evaluate-operator*elaborate*visited*current-location
   (state <s> ^name selection
              ^operator <op>
              ^superstate.top-state.current-location <wp>
)#              ^evaluation <e>)
   (<op> ^name evaluate-operator
         ^evaluation <e>)
-->
   (<e> ^visited <wp>)
}

sp {selection*elaborate*state*all-objects-total-estimated-cost
   :default
   (state <s> ^name selection)
   (<s> ^operator <o> +)
   (<o> ^name evaluate-operator
       -^evaluation.total-estimated-cost)
-->
#   (write (crlf) |Not all operators total estimated cost|)
   (<s> ^not-all-operators-total-estimated-cost true)}

#sp {selection*elaborate*estimated-cost
#   :default
#   (state <s> ^name selection
#              ^evaluation <e>
#              ^superstate.desired <dloc>)
#   (<e> ^current-location <cloc>)
#   (<cloc> ^x <cx> ^y <cy> ^id <cid>)
#   (<dloc> ^x <dx> ^y <dy> ^id <did>)
#-->
#   (write (crlf) |*********************************|)
#   (write (crlf) |Current waypoint: | <cid> | | <cx> | | <cy>)
#   (write (crlf) |Desired waypoint: | <did> | | <dx> | | <dy>)
#   (write (crlf) |Estimated cost:   | (compute-range <cx> <cy> <dx> <dy>))
#   (<e> ^estimated-cost (compute-range <cx> <cy> <dx> <dy>))
#}
#
sp {selection*elaborate*evaluation*estimated-cost
   :default
   (state <s> ^name selection
              ^evaluation <e>
             -^top-state.current-location <cloc>
              ^superstate.desired.location <dloc>)
   (<e> ^current-location <cloc>
       -^updated-estimate true)
   (<cloc> ^x <cx> ^y <cy> ^id <cid>)
   (<dloc> ^x <dx> ^y <dy> ^id <did>)
-->
   (write (crlf) |****************************|)
#   (write (crlf) |current x | <cx> | y | <cy>)
#   (write (crlf) |desired x | <dx> | y | <dy>)
#   (write (crlf) |range:    | (compute-range <cx> <cy> <dx> <dy>))
   (write (crlf) |Estmated Cost: | (compute-range <cx> <cy> <dx> <dy>) | : | <e> | : | <cid>)
   (<e> ^estimated-cost (compute-range <cx> <cy> <dx> <dy>)  )
}
sp {selection*elaborate*evaluation*total-estimated-cost
   :default
   (state <s> ^name selection
              ^evaluation <e>)
   (<e> ^estimated-cost <ec>
        ^path-cost { <> 0 <pc> })
-->
   (write (crlf) |++++++++++++++++++++++++++++++|)
   (write (crlf) |Total Estmated Cost: | (+  <ec> <pc>) | : | <e>)
   (<e> ^total-estimated-cost (+  <ec> <pc>))
}

sp {selection*elaborate*evaluation*final-cost
   :default
   (state <s> ^name selection
              ^evaluation <e>)
   (<e> ^estimated-cost 0
        ^path-cost <pc>)
-->
   (write (crlf) |Final Cost: | <pc> | : | <e>)
   (<e> ^final-cost <pc>
        ^value true)
}

sp {selection*elaborate*superstate*preferences*final-cost*equal
   :default
   (state <s> ^name selection
              ^evaluation <e1> { <e2> <> <e1> }
              ^superstate <ss>)
   (<e1> ^superoperator <so1>
         ^final-cost <fc>)
   (<e2> ^superoperator <so2>
         ^final-cost <fc>)
-->
   (<ss> ^operator <so1> = <so2>)}

# probably redundant with following rule
sp {selection*elaborate*superstate*preferences*final-cost*better
   :default
   (state <s> ^name selection
              ^evaluation <e1> { <e2> <> <e1> }
              ^superstate <ss>)
   (<e1> ^superoperator <so1>
         ^final-cost <fc>)
   (<e2> ^superoperator <so2>
         ^final-cost > <fc>)
-->
   (<ss> ^operator <so1> > <so2>)}


sp {selection*elaborate*superstate*preferences*final-cost*better*estimated
   :default
   (state <s> ^name selection
              ^evaluation <e1> { <e2> <> <e1> }
              ^superstate <ss>)
   (<e1> ^superoperator <so1>
         ^final-cost <fc>)
   (<e2> ^superoperator <so2>
         ^total-estimated-cost > <fc>)
-->
   (<ss> ^operator <so1> > <so2>)}