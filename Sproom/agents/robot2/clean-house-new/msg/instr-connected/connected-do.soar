sp {instr-connected*propose*connected-do
   (state <s> ^name instr-connected
              ^superstate.operator <ss-op>
              ^top-state <ts>)
   (<ss-op> ^id1 <id1>
            ^id2 <id2>
            ^dir <dir>)
   (<ts> ^areas <areas>)
   (<areas> ^area <a2>)
   (<a2> ^id <id2>)
  -{(<areas> ^area <a1>)
    (<a1> ^id <id1>)}
-->
   (<s> ^operator <op> + =)
   (<op> ^name connected-do
         ^known <a2>
         ^unknown <id1>
         ^dir <dir>)
}

sp {apply*connected-do*connect
   (state <s> ^operator <op>
              ^top-state <ts>)
   (<op> ^name connected-do
         ^known <a2>
         ^unknown <id1>
         ^dir <dir1>)
   (<a2> ^id <id2>)
   (<ts> ^areas <areas>
         ^directions <d>)
   (<d> ^value <dir1>
        ^opposite <dir2>)
-->
   (<op> ^status complete)
   (<areas> ^area <a1>)
   (<a1> ^id <id1>
         ^neighbor-area <a2>
         ^gateway <g1>)
   (<a2> ^neighbor-area <a1>
         ^gateway <g2>
         ^type door)
   (<g1> ^type instruction
         ^to <id2>
         ^direction <dir2>)
   (<g2> ^type instruction
         ^to <id1>
         ^direction <dir1>)
}

sp {apply*connected-do*complete
   (state <s> ^operator <op>
              ^superstate.operator <ss-op>)
   (<op> ^name connected-do
         ^status complete)
-->
   (<ss-op> ^done true)
}
